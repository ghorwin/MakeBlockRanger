:imagesdir: ../images

[[chap:motors]]
# Motorsteuerung

Jetzt wird endlich mal was bewegt. In diesem Teil geht es um die Programmierung der Motoren. 

Zunächst aber mal ein konstruktiver Tipp:

[TIP]
====
Die Ketten sind am Anfang ewas steif und durch die Lagerung und Verpackung etwas oval verformt. Wenn man den _Land Raider_ wie in der Konstruktionszeichnung zusammen baut, dann sitzen die Ketten _sehr_ straff und jedes Mal, wenn der Motor braucht immer viel Kraft, um aus der gekrümmten Kettenform rauszudrehen. Bei der vorgegebenen Firmware führt das dann beim Steuern des _Land Raider_ häufig dazu, dass er mit einem Beepen stehen bleibt (warum die Firmware das macht, erklär ich unten).

Es gibt zwei Dinge, die man tun kann, um dieses Problem mit dem _Land Raider_ zu beheben:

- die Ketten "rund" dehnen, z.B. auf eine ausreichend dicke Blumevase schieben, bis sie leicht gespannt sind, und dann eine Weile in die Sonne stellen und ggfs. gelegentlich nachspannen
- die Motoren um eine Bohrung nach Innen befestigen, wie auf dem nachfolgenden Bild zu sehen ist

.Um ein Loch verschobene Motormontage zur Reduktion der Spannung in den Ketten
image::raider_motor_verschoben.jpg[]
====


## Allgemeines

Am Auriga-Board gibt es zwei Motorports. Jeder der Motoren kann über einen PWM Pin (für die Geschwindigkeit) und zwei Richtungspins angesteuert werden. Die Ansteuerung kann einmal direkt über die Arduino-Pins erfolgen oder unter Verwendung der Makeblock Bibliothek.

[NOTE]
====
Grundsätzlich bewegen sich die Motoren nur, wenn die Spannungsversorgung über die Batterien erfolgt (die Motoren brauchen eine Spannung > 7V, was der USB-Port ja nicht liefern kann). Das ist auch ganz praktisch, da man so den Robotor angesteckt an den Computer programmieren kann, ohne Angst zu haben, dass er vom Tisch rollt.
====

Die Motoren werden an die beiden Motorports angesteckt:

.Motor-Ports an der Auriga-Platine
image::auriga_motorports.png[]

Zunächst beschreibe ich mal die Programmierung der Motoren ohne die MakeBlock-Bibliotheksklassen. Dadurch versteht man so besser versteht, wie das alles zusammen funktioniert. Danach beschreibe ich in <<sec:motor:makeblocklib>> die Funktionalität der MakeBlock-Bibliotheksklasse zur Encoder-Motor-Programmierung.


## Direkte Ansteuerung mit Arduino-Hausmitteln

Aus dem Schaltplan kann man die Bezeichnung der digitalen Pins (D46...D49) und die PWM Pins (D10, D11) entnehmen:

.Schaltplanauszug für die Motoransteuerung
image::auriga_schaltplanauszug_motor.png[]

Die digitalen Pins werden nun wie folgt angesprochen:

- PWMA und PWMB erhalten Drehgeschwindigkeiten (PWM, im Bereich 0..255)
- DIRA1 und DIRA2 regeln die Richtung des Motors A (links) und müssen jeweils entgegengesetzt geschaltet sein
- DIRB1 und DIRB2 regeln die Richtung des Motors B (rechts) und müssen jeweils entgegengesetzt geschaltet sein


### Beispielprogramm - Beschleunigen und Abbremsen

```c++
// Testprogramm für die Ansteuerung der Motoren via Arduino Pins

#define PWMA  11 // Motor A/Links
#define DIRA1 49
#define DIRA2 48
#define PWMB  10 // Motor B/Rights
#define DIRB1 47
#define DIRB2 46

void setup() {
  // PWM und Richtungspins setzen
  pinMode(PWMA,  OUTPUT);  
  pinMode(DIRA1, OUTPUT); 
  pinMode(DIRA2, OUTPUT);
  pinMode(PWMB,  OUTPUT);  
  pinMode(DIRB1, OUTPUT); 
  pinMode(DIRB1, OUTPUT);

  // kurze Wartephase zur Initialisierung
  delay(20);
}

void setLeftMotorSpeed(int speed) {
  if (speed >= 0) {
    analogWrite (PWMA,  speed);
    digitalWrite(DIRA1, LOW);
    digitalWrite(DIRA2, HIGH);
  }
  else {
    analogWrite (PWMA,  -speed);
    digitalWrite(DIRA1, HIGH);
    digitalWrite(DIRA2, LOW);
  }
}

void setRightMotorSpeed(int speed) {
  if (speed >= 0) {
      analogWrite (PWMB,  speed);
      digitalWrite(DIRB1, HIGH);
      digitalWrite(DIRB2, LOW);
  }
  else {
    analogWrite (PWMB,  -speed);
    digitalWrite(DIRB1, LOW);
    digitalWrite(DIRB2, HIGH);
  }
}


// Maximalgeschwindigkeit
int maxSpeed = 255;
int speed = 0;
bool upward = true;

void loop() {

  // Steuerungsprogramm: 
  // - Vorwärtsfahren und Geschwindigkeit in jeder Schleife erhöhen bis Maximalgeschwindikeit erreicht ist
  // - dann langsamer werden und schließlich Rückwärtsfahren bis maximale Rückwärtsgeschwindigkeit erreicht ist
  // - dann wieder umschalten auf vorwärts fahren
  if (upward)
    speed += 5;
  else
    speed -= 5;
  if (speed > maxSpeed) {
    upward = false;
    speed = maxSpeed;
  }
  if (speed < -maxSpeed) {
    upward = true;
    speed = -maxSpeed;
  }

  // Motorgeschwindigkeiten anpassen
  setLeftMotorSpeed(speed);
  setRightMotorSpeed(speed);

  delay(50);
}
```

Hier fährt der Roboter einfach nur vor und zurück und beschleunigt/bremst dabei.

### Beispielprogramm 2 - Konstante Fahrt vorwärts

Man kann das Programm auch vereinfachen, und den Roboter einfach mit konstanter Geschwindigkeit geradeaus fahren lassen. Dabei werden beide Motoren mit dem gleichen PWM-Signal angesteuert.


```c++
// Testprogramm für die Ansteuerung der Motoren via Arduino Pins

#define PWMA  11 // Motor A/Links
#define DIRA1 49
#define DIRA2 48
#define PWMB  10 // Motor B/Rights
#define DIRB1 47
#define DIRB2 46

void setup() {
  // PWM und Richtungspins setzen
  pinMode(PWMA,  OUTPUT);  
  pinMode(DIRA1, OUTPUT); 
  pinMode(DIRA2, OUTPUT);
  pinMode(PWMB,  OUTPUT);  
  pinMode(DIRB1, OUTPUT); 
  pinMode(DIRB1, OUTPUT);

  // kurze Wartephase zur Initialisierung
  delay (20);

  // links vorwärts, 100 PWM
  analogWrite (PWMA,  100);
  digitalWrite(DIRA1, LOW);
  digitalWrite(DIRA2, HIGH);

  // rechts rückwärts, 100 PWM
  analogWrite (PWMB,  100);
  digitalWrite(DIRB1, HIGH);
  digitalWrite(DIRB2, LOW);
}

// in der Hauptschleife machen wir nix
void loop() {
}
```

Nun _sollte_ der Raider schön geradeaus fahren... macht er aber gelegentlich nicht. Warum? Weil je nach Steifigkeit der Kette oder Untergrund oder anderen Reibungsfaktoren sich die Motoren trotz gleichem PWM-Signal nicht gleich schnell drehen. Um das zu beheben und wirklich präzise zu navigieren, müsste man die Motorleistung so anpassen, dass die geforderte Drehzahl erreicht wird. Dafür müsste man aber herausfinden, wie schnell sich die Motoren _tatsächlich_ drehen. Und genau das kann man bei den Encoder-Motoren machen.

## Motordrehgeschwindigkeit mittels Encoder ermitteln

Der Encoder im Motor ist ein Drehimpulsgeber/Tacho und liefert elektrische Impulse, die man zählen kann. Damit kann man die Geschwindigkeit und zurückgelegte Entfernung bestimmen. Zum Einstieg in das Thema _Encoder_ kann ich das https://www.youtube.com/watch?v=-SFIOm5mt4U[Encoder Tutorial Arduino (deutsch, für Anfänger)] empfehlen.

Der Encoder eines Motors ist am Arduino an jeweils zwei digitalen Pins angeklemmt. Um einen Drehimpuls zu erfassen, muss man den einen Pin überwachen und feststellen, wann dieser von 0 auf 1 wechselt. Damit man das präzise machen kann, ohne dass anderen Aufgaben im Mikroprozessor die Messung beeinträchtigen, verwendet man Interruptfunktionen. Dazu registriert man für jeden Motor eine Interruptfunktion, die immer dann aufgerufen wird, wenn am entsprechenden Pin ein RISING Signal anliegt (also der Eingangspin von LOW auf HIGH wechselt). In der Interruptfuktion wird dann ein Zähler hochgezählt, der dann für die Berechnung der Momentangeschwindigkeit und Bewegung verwendet wird. 


### Beispielprogramm 3 - Variable Motordrehzahl und Geschwindigkeit via Encoder auslesen

Nun folgt ein etwas längeres Beispielprogramm, welches aber unten detailliert erklärt wird.

```c++
// Testprogramm für die Ansteuerung der Encoder-Motoren via Arduino Pins

// Motor A/Links
#define PWMA  11 
#define DIRA1 49
#define DIRA2 48

#define ENCODER1_PIN1 19  // Interrupt-Pin für Encoder 1
#define ENCODER1_PIN2 42

// Motor B/Rechts

#define PWMB  10 
#define DIRB1 47
#define DIRB2 46

#define ENCODER2_PIN1 18  // Interrupt-Pin für Encoder 2
#define ENCODER2_PIN2 43

#define PULSESPERREVOLUTION 353.403

int targetSpeedPWM;                   // Ziel-PWM für beide Motoren
unsigned long startMillis;            // Millis am Programmstart
unsigned long pulseCountStartMillis;  // Millis beim Start der Pulszählung
unsigned long lastOutputMillis;       // Millis bei der letzten Ausgabe

long encoder1Pulses;                  // gezählte Pulse des Encoders 1 (können negativ werden)
long encoder2Pulses;                  // gezählte Pulse des Encoders 2 (können negativ werden)

// Für präzise Messung der Geschwindigkeit (und damit Position) der Motoren 
// werden die Pulse via Interruptfunktionen hoch/runtergezählt.
// Jedes Mal, wenn der Interrupt-Pin eines Encoders von LOW auf HIGH wechselt (RISING),
// wird die jeweilige isr_process_encoderX() Funktion aufgerufen und zählt einen 
// Pulse hoch.

// Interruptfunktion für Encoder 1
void encoder1_pulse(void) {
  if (digitalRead(ENCODER1_PIN2) == 0)
    ++encoder1Pulses;
  else
    --encoder1Pulses;
}

// Interruptfunktion für Encoder 2
void encoder2_pulse(void) {
  if (digitalRead(ENCODER2_PIN2) == 0)
    --encoder2Pulses;   // ACHTUNG: Drehrichtung des 2. Motors ist andersherum
  else
    ++encoder2Pulses;
}

// Umrechung von Pulsen zu Umdrehungen pro Minute (RPM)
float rpmFromPulses(long pulses) {
  unsigned long deltaT = millis() - pulseCountStartMillis; // Zeit seit Beginn der Zählung
  float pulsePerSecond = pulses*1000.0/deltaT;
  float rpmPerMinute = pulsePerSecond*60/PULSESPERREVOLUTION; // 353.403 Pulse pro Umdrehung
  return rpmPerMinute;
}

// setzt Geschwindigkeit für linken Motor (-255...255)
void setLeftMotorSpeed(int targetPWM) {
  if (targetPWM >= 0) {
    analogWrite (PWMA,  targetPWM);
    digitalWrite(DIRA1, LOW);
    digitalWrite(DIRA2, HIGH);
  }
  else {
    analogWrite (PWMA,  -targetPWM);
    digitalWrite(DIRA1, HIGH);
    digitalWrite(DIRA2, LOW);
  }
}

// setzt Geschwindigkeit für rechten Motor (-255...255)
void setRightMotorSpeed(int targetPWM) {
  if (targetPWM >= 0) {
      analogWrite (PWMB,  targetPWM);
      digitalWrite(DIRB1, HIGH);
      digitalWrite(DIRB2, LOW);
  }
  else {
    analogWrite (PWMB,  -targetPWM);
    digitalWrite(DIRB1, LOW);
    digitalWrite(DIRB2, HIGH);
  }
}


void setup() {
  // PWM und Richtungspins setzen
  pinMode(PWMA,  OUTPUT);  
  pinMode(DIRA1, OUTPUT); 
  pinMode(DIRA2, OUTPUT);
  pinMode(PWMB,  OUTPUT);  
  pinMode(DIRB1, OUTPUT); 
  pinMode(DIRB1, OUTPUT);

  // WICHTIG: INPUTs mit PullUp Widerständen!
  pinMode(ENCODER1_PIN1, INPUT_PULLUP);   
  pinMode(ENCODER1_PIN1, INPUT_PULLUP); 
  pinMode(ENCODER2_PIN2, INPUT_PULLUP); 
  pinMode(ENCODER2_PIN2, INPUT_PULLUP); 

  // kurze Wartephase zur Initialisierung
  delay (20);

  // Festlegen der Interruptfunktionen für das Messen/Zählen der Bewegung
  attachInterrupt(digitalPinToInterrupt(ENCODER1_PIN1), encoder1_pulse, RISING);
  attachInterrupt(digitalPinToInterrupt(ENCODER2_PIN1), encoder2_pulse, RISING);

  // Interne Timer für PWM auf 8KHz programmieren
  TCCR1A = _BV(WGM10);
  TCCR1B = _BV(CS11) | _BV(WGM12);

  TCCR2A = _BV(WGM21) | _BV(WGM20);
  TCCR2B = _BV(CS21);

  // Variable für gelegentliche Ausgaben auf die serielle Schnittstelle
  lastOutputMillis = startMillis = pulseCountStartMillis = millis();

  encoder1Pulses = 0;
  encoder2Pulses = 0;

  Serial.begin(115200);
}


// in der Hauptschleife verändern wir je nach Eingabe der Geschwindigkeit 
// das Tempo und geben regelmäßig Zielwert und tatsächliche Geschwindigkeit aus
void loop() {
  // von der seriellen Schnittstelle lesen
  if (Serial.available()) {
    // Zeichen 0...6 werden gelesen
    char a = Serial.read();
    switch(a) {
      case '0': targetSpeedPWM =    0; break;  // stop
      case '1': targetSpeedPWM =  100; break;  // slow forward
      case '2': targetSpeedPWM =  200; break;  // fast forward
      case '3': targetSpeedPWM =  255; break;  // fastest forward
      case '4': targetSpeedPWM = -100; break;  // slow backward
      case '5': targetSpeedPWM = -200; break;  // fast backward
      case '6': targetSpeedPWM = -255; break;  // fastest backward
    }
    setLeftMotorSpeed(targetSpeedPWM);
    setRightMotorSpeed(targetSpeedPWM);
  }
  // alle 100 Millisekunden die Geschwindigkeit ausgeben
  if (millis() > lastOutputMillis + 100) {
    lastOutputMillis = millis();
    // Ausgabe: Zeit [s]  \t Ziel-PWM \t Speed 1 [rpm] \t Speed 2 [rpm]
    Serial.print((millis() - startMillis)*0.001);
    Serial.print("\t");
    Serial.print(targetSpeedPWM);
    Serial.print("\t");
    Serial.print(rpmFromPulses(encoder1Pulses));
    Serial.print("\t");
    Serial.println(rpmFromPulses(encoder2Pulses));
    // die Counter zurücksetzen
    encoder1Pulses = 0;
    encoder2Pulses = 0;
    pulseCountStartMillis = millis();
  }
}
```

In diesem Programm hat viele Bestandeile, die man aber am Besten nach und nach erklärt.

### Interruptfunktionen für Encoder-Pins und Geschwindigkeitsberechnung

Wie schon oben erwähnt, werden Interruptfunktionen mit den Interrupt-Pins der Encoder verknüpft, wobei auf den Wechsel des Pegels von low auf high reagiert wird (`RISING`). Das passiert in der `setup()` Funktion:

```c++
  // Festlegen der Interruptfunktionen für das Messen/Zählen der Bewegung
  attachInterrupt(digitalPinToInterrupt(ENCODER1_PIN1), isr_encoder1, RISING);
  attachInterrupt(digitalPinToInterrupt(ENCODER2_PIN1), isr_encoder2, RISING);
```

[NOTE]
====
Der Funktionspräfix `isr` steht für _Interrupt Service Routine_.
====

Auf dem Mega 2560 gibt es mehrere Interrupt-fähige Pins, unter anderem Pin 18 und 19, welche mit dem Encoder verbunden sind (siehe auch Pinout-Diagramm, M1 und M2, wo D19/INT1 und D18/INT3 steht). Diesen Pins wird im Mega 2560 jeweils eine Interrupt-Nummer zugewiesen. Mit der Funktion `digitalPinToInterrupt()` erhält man die zu einem Interrupt-fähigen Pin zugehörige Interrupt-Nummer:

```c
int interruptNr1 = digitalPinToInterrupt(19);  // -> interrupt 4
int interruptNr2 = digitalPinToInterrupt(18);  // -> interrupt 5
```

Siehe auch Erklärung zu den Interrupt-Nummern in https://docs.arduino.cc/language-reference/funktionen/external-interrupts/attachInterrupt/


Wie bei Encodern üblich wird die Flanke des einen Pins überwacht und dann durch den (etwas versetzten Pegel des 2. Pins) die Drehrichtung bestimmt. Dies geschicht in der Interrupt-Funktion:

```c
// Interruptfunktion für Encoder 1
void isr_encoder1(void) {
  if (digitalRead(ENCODER1_PIN2) == 0)
    ++encoder1Pulses;
  else
    --encoder1Pulses;
}
```
In den Interrupt-Funktionen wird jetzt bei jedem Aufruf ein Puls hoch oder runter gezählt. Da der Motor 2 sich anders herum dreht, wird entsprechend invertiert gezählt (sodass positive Pulszahlen einer Vorwärtsfahrt entsprechen).

Die Funktion `rpmFromPulses()` rechnet nun die bislang gezählten Pulse in eine Drehzahl um. Die seit Beginn der Zählung verstrichene Zeit wird berechnet und dann wird auf Pulse pro Minute hochgerechnet. Die Encoder geben bei einer Umdrehung 353.403 Pulse ab.

[NOTE]
====
Die Encodermotoren drehen sich eigentlich bei Maximalgeschwindigkeit mit ca. 14000 Umdrehungen pro Minute (rpm). Das eingebaute Getriebe hat laut Spezifikation ein Verhältnis von 39.6, sodass sich das angesteckte Rad selbst eher mit 350 rpm dreht. In der Makeblock-Bibliothek ist ein Getriebeverhältnis von 39.267 angegeben, also hab ich diesen Wert verwendet. 

Bei jeder Umdrehung des Motors werden 9 Pulse abgegeben. Damit ergeben sich bei einer Umdrehung des Rades die 9*39.267=353.403 Pulse.
====

Nach Berechnung der Geschwindigkeit wird ein neues Zählintervall gestartet, indem der Startzeitpunkt aktualisiert wird und die Pulszählvariablen auf 0 gesetzt werden:

```c
// die Counter zurücksetzen
encoder1Pulses = 0;
encoder2Pulses = 0;
pulseCountStartMillis = millis();
```


### Erhöhung der Messgenauigkeit

Der MEGA 2560 Prozessor hat intern Timer, die letztlich die Abtastrate für die Überwachung der Interrupt-Pins definieren. Normalerweise haben diese eine recht niedrige Frequenz. 

Theoretisch müssten bei 14000 rpm und 9 Pulsen-pro-Umdrehung 14000*9/60 = 2100 Pulse/Sekunde erfasst werden. Damit man die Pulse erkennt, müsste jeweils LOW und HIGH gemessen werden, also 4200 Messwerte pro Sekunde. Dafür bräuchte man also ein Abtastrate von 4.2 kHz.

Die Timer-Frequenzen kann man umprogrammieren (siehe auch Erklärung in https://wolles-elektronikkiste.de/interrupts-teil-3-timer-interrupts#calculations[Interrupts – Teil 3: Timer Interrupts] und beispielsweise die konkret verwendete Bitmaske für Timer 1 auf 
https://dbuezas.github.io/arduino-web-timers/#mcu=ATMEGA328P&timer=1&timerMode=FPWM&clockPrescalerOrSource=8[Arduino Web Timers]).

Der relevante Code-Block ist:

```c
// Interne Timer für PWM auf 8KHz programmieren (Mode 5 - Fast PWM, 8-Bit, Prescaler 8)
// Timer 1
TCCR1A = _BV(WGM10);
TCCR1B = _BV(CS11) | _BV(WGM12);

// Timer 2
TCCR2A = _BV(WGM21) | _BV(WGM20);
TCCR2B = _BV(CS21);
```

`_BV(X)` steht für einen Bitshift `1 << X`.
 
[NOTE]
====
In meinen Tests habe ich selbst bei komplett aufgeladenen Batterien und Vollspeed kaum einen Unterschied in den gemessenen Pulsraten bzw. rpm gesehen, ungeachtet dessen, ob die Timer umprogrammiert wurden oder nicht.
====




[[sec:motor:makeblocklib]]
## Ansteuerung über Encoder-Klasse der Bibliothek

Die MakeBlock-Bibliothek bietet zahlreiche Klassen für die Programmierung von Encodern. Für die mitgelieferten DC-Motoren wird die Klasse `MeEncoderOnBoard` verwendet. Diese übernimmt, wie der Name schon sagt, die Programmierung der auf dem Board befindlichen Motorsteuerung. Diese Klasse erlaubt sowohl die direkte Ansteuerung der Motoren durch setzen der Leistung via PWM, als auch die geregelte Steuerung.

[[sec:motor:encodertest]]
### Beispielprogramm - Ansteuerung mittels Bibliotheksfunktion

Im Folgenden Testbeispiel kann man den Robotor begrenzt steuern:

- Nach Start des Programms den SerialMonitor der Arduino-IDE aufrufen
- in die Zeile "Nachricht" des SerialMonitor eine Zahl 0...6 eintippen und mit Enter abschicken
- das Programm wertet diese Zahl aus und legt die Drehgeschwindigkeit der Motoren fest

Das Testbeispiel enthält eine Menge Details, welche weiter unten erklärt werden.

.Kleines Testprogramm für die Motorsteuerung und Messung der Geschwindigkeit mittels der Encoder-Bibliothek
```c++
// Testprogramm für die Ansteuerung der DC Motoren via Makeblock Bibliothek
#include <MeAuriga.h>

MeEncoderOnBoard Encoder_1(SLOT1);
MeEncoderOnBoard Encoder_2(SLOT2);

// Für präzise Messung der Geschwindigkeit (und damit Position) der Motoren 
// werden die Pulse via Interruptfunktionen hoch/runtergezählt.
// Jedes Mal, wenn der Interrupt-Pin eines Encoders von LOW auf HIGH wechselt (RISING),
// wird die jeweilige isr_process_encoderX() Funktion aufgerufen und zählt einen 
// Pulse hoch.

// Interruptfunktion für Encoder 1
void isr_process_encoder1(void) {
  if (digitalRead(Encoder_1.getPortB()) == 0)
    Encoder_1.pulsePosMinus();
  else
    Encoder_1.pulsePosPlus();
}

// Interruptfunktion für Encoder 2
void isr_process_encoder2(void) {
  if (digitalRead(Encoder_2.getPortB()) == 0)
    Encoder_2.pulsePosMinus();
  else
    Encoder_2.pulsePosPlus();
}


int lastOutputMillis;
int targetSpeedPWM;

void setup() {
  // Festlegen der Interruptfunktionen für das Messen/Zählen der Bewegung
  attachInterrupt(Encoder_1.getIntNum(), isr_process_encoder1, RISING);
  attachInterrupt(Encoder_2.getIntNum(), isr_process_encoder2, RISING);
  Serial.begin(115200);
  
  // Interne Timer der PWM auf 8KHz programmieren
  TCCR1A = _BV(WGM10);
  TCCR1B = _BV(CS11) | _BV(WGM12);

  TCCR2A = _BV(WGM21) | _BV(WGM20);
  TCCR2B = _BV(CS21);

  // Variable für gelegentliche Ausgaben auf die serielle Schnittstelle
  lastOutputMillis = millis();
  targetSpeedPWM = 0;
}

void loop() {
  // von der seriellen Schnittstelle lesen
  if (Serial.available()) {
    // Zeichen 0...6 werden gelesen
    char a = Serial.read();
    switch(a) {
      case '0': targetSpeedPWM =    0; break;  // stop
      case '1': targetSpeedPWM =  100; break;  // slow forward
      case '2': targetSpeedPWM =  200; break;  // fast forward
      case '3': targetSpeedPWM =  255; break;  // fastest forward
      case '4': targetSpeedPWM = -100; break;  // slow backward
      case '5': targetSpeedPWM = -200; break;  // fast backward
      case '6': targetSpeedPWM = -255; break;  // fastest backward
    }
    Encoder_1.setTarPWM(-targetSpeedPWM);
    Encoder_2.setTarPWM(targetSpeedPWM);
  }

  // In der loop() Funktion wird die Geschwindigkeit im Motor geregelt
  Encoder_1.loop();
  Encoder_2.loop();

  // alle 100 Millisekunden die Geschwindigkeit ausgeben
  if (millis() > lastOutputMillis + 100) {
    lastOutputMillis = millis();
    Serial.print("Target speed [PWM]:");
    Serial.print(targetSpeedPWM);
    // Erreichte Geschwindigkeit
    Serial.print(",Speed 1:");
    Serial.print(-Encoder_1.getCurrentSpeed());
    Serial.print(",Speed 2:");
    Serial.println(Encoder_2.getCurrentSpeed());
  }
}
```

In diesem Code werden einige fortgeschrittene Arduino-Programmier-Techniken verwendet, die nachfolgend etwas genauer beleuchtet werden.


### Interrupts für Encoder-Signale

Die in den Motoren verbauten Encoder liefern an jeweils 2 Pins die Encoder-Signale aus, wobei jeweils einer der Pins ein Interrupt-fähiger Pin ist. Zunächst werden die Funktionen `isr_process_encoder1()` und `isr_process_encoder2()` an Interrupts gebunden, wobei auf den Wechsel des Pegels von low auf high reagiert wird (`RISING`):

```c++
// Verknüpfung einer Interrupt-Funktion
attachInterrupt(Encoder_1.getIntNum(), isr_process_encoder1, RISING);
```

Die Funktion `Encoder_1.getIntNum()` liefert dabei die Interruptnummer für den interruptfähigen Pin.

[NOTE]
====
Auf dem Mega 2560 gibt es mehrere Interrupt-fähige Pins, unter anderem Pin 18 und 19, welche mit dem Encoder verbunden sind (siehe auch Pinout-Diagramm, M1 und M2, wo D19/INT1 und D18/INT3 steht. Diesen Pins wird im Mega 2560 jeweils eine Interrupt-Nummer zugewiesen. Normalerweise bekommt man die über die Funktion `digitalPinToInterrupt()` die zu einem Interrupt-fähigen Pin zugehörige Internetnummer und genau dies liefert die Funktion `getIntNum()`.

```c
int interruptNr1 = digitalPinToInterrupt(19);  // -> interrupt 4, genau wie Encoder_1.getIntNum()
int interruptNr2 = digitalPinToInterrupt(18);  // -> interrupt 5, genau wie Encoder_2.getIntNum()
```

Siehe auch Erklärung zu den Interrupt-Nummern in https://docs.arduino.cc/language-reference/funktionen/external-interrupts/attachInterrupt/
====


Wie bei Encodern üblich wird die Flanke des einen Pins überwacht, und dann durch den (etwas versetzten Pegel des 2. Pins) die Drehrichtung bestimmt. Dies geschicht in der Interrupt-Funktion:

```c
void isr_process_encoder1(void) {
  // Encoder_1.getPortA() -> Pin 19
  // Encoder_1.getPortB() -> Pin 42
  
  // wenn der 2. Encoder-Pin gleichzeitig High ist, dreht sich der Encoder vorwärts
  if (digitalRead(Encoder_1.getPortB()) == 0)
    Encoder_1.pulsePosMinus();
  else
    // sonst rückwärts
    Encoder_1.pulsePosPlus();
}
```

Die Encoder zählen jetzt also bei jedem Aufruf einen Puls höher oder runter. In der in jedem Durchlauf aufgerufenen Funktion `MeEncoderOnBoard::loop()` wird die Funktion `MeEncoderOnBoard::updateSpeed()` aufgerufen, welche letztlich die Anzahl der Pulse seit dem letzten Aufruf von `updateSpeed()` in die Drehgeschwindigkeit umrechnet.

### Testlauf und Geschwindigkeitsmessung mit und ohne Kette

Wenn man jetzt das Programm mal testet, zuerst noch ohne den Codeblock in der `setup()` Funktion mit den umprogrammierten PWM-Timern, und die Geschwindigkeiten plottet erhält man ein interessantes Bild:

.Ausgabe der Motorgeschwindigkeiten mit originalen PWM-Timern und jeweils einen Motor mit Kette und den anderen Motor freidrehend
image::auriga_encoder_speed_original_PWM_timer_one_belt.png[]

Da die Kette schwingt und auch nicht gleichmäßig steif ist, führt dies zu ungleichmäßigen Drehbewegungen des Motors. Um diesen Einfluss zu sehen, habe ich an einen Motor die Kette angebaut und am anderen Motor nicht. Klar erkennbar im Diagramm ist auch die Bremswirkung dder Kette insgesamt - der Motor mit Kette dreht langsamer.

Außerdem wird die Ziel-PWM-Geschwindigkeit offensichtlich nicht ganz erreicht. Dies liegt daran, dass das generierte PWM-Signal zu langsam ist. Um dies zu beheben, kann der PWM-Timer des Arduino umprogrammiert werden, sodass er mit 8kHz läuft. Dies passiert im Codeblock:

```c
  // Interne Timer der PWM auf 8KHz programmieren
  TCCR1A = _BV(WGM10);
  TCCR1B = _BV(CS11) | _BV(WGM12);

  TCCR2A = _BV(WGM21) | _BV(WGM20);
  TCCR2B = _BV(CS21);
```

Erneut getestet sieht die Geschwindigkeitsausgabe deutlich besser aus, d.h. auch die durch die Encoder erfasste Drehzahl ist auch bei kleineren Drehzahlen hinreichend proportional zur Sollgeschwindigkeit (in PWM).

.Ausgabe der Motorgeschwindigkeiten (in Umdrehungen pro Minute) mit umprogrammierten PWM-Timern und jeweils einen Motor mit Kette und den anderen Motor freidrehend
image::auriga_encoder_speed_8kHz_PWM_timer_one_belt.png[]

Beim freilaufenden Motor wird die festgelegte Soll-Geschwindigkeit offenbar gut erreicht. Beim Motor mit Kette bleibt die Sollgeschwindigkeit etwas hinter der festgelegten Geschwindigkeit, was ja an der Bremswirkung liegt.

Im Beispiel oben wurde die Geschwindigkeit in PWM angegeben und die Drehzahl vom Encoder in Umdrehungen pro Minute (RPM) abgelesen.
Die Verhältnisse sind

  132/100 = 1.32
  266/200 = 1.33
  339/255 = 1.33
  
Damit kann man also die geforderte Drehzahl im Bereich -340...340 mit der Formel einstellen:

  pwm = rpm/1.33



Die Drehzahl eines unbelasteten Rades alleine sagt ja noch nichts über die Fahrgeschwindigkeit des Roboters aus. Dazu muss man erstmal Testmessungen machen. Und natürlich hängt die Geschwindigkeit dann auch noch vom Batterieladezustand ab und vom Anstieg und von der Reibung der Kette und und und... Deshalb ist es sinnvoller, die Leistung anhand der geforderten Geschwindigkeit einzuregeln. Doch dazu später mehr... 

[TIP]
====
Wenn man den Land Raider mit aufgeladenen Batterien und Maximalgeschwindigkeit fahren lässt, so schafft er bei vollen Batterien auf gerader Fläche immerhin **0,56 m/s** bzw. **2 km/h**. Das reicht locker, um durch eine Legostadt zu düsen und spektakuläre Videos aus der Legomännelperspektive zu filmen.
====


## Motorleistung adaptiv regeln mittels in der Encoder-Klasse

Die Ansteuerung via setzen der Ziel-PWM ist di einfachste und direkteste Möglichkeit, den Roboter zu steuern. Aber wie oben erwähnt kann man so kaum eine geforderte Fahrgeschwindigkeit bestimmen. Die Klasse `MeEncoderOnBoard` bietet aber noch eine zweite Variante, wie man die Geschwindigkeit bzw. Motorleistung definieren kann: einen klassischen PID Regler-Ansatz.

Hierbei wird die aktuelle Geschwindigkeit andauernd mit der Sollgeschwindigkeit verglichen. Die Abweichung (der Regelfehler) wird dazu benutzt, um die Leistung/PWM der Motoren solange anzupassen, bis die Ist-Geschwindigkeit der Soll-Geschwindigkeit entspricht und der Regelfehler (nahezu) 0 wird.

.Beispielprogramm für die adaptive Regelung der Geschwindigkeit
```c++
// Testprogramm für die Ansteuerung der DC Motoren via Makeblock Bibliothek
#include <MeAuriga.h>

MeEncoderOnBoard Encoder_1(SLOT1);
MeEncoderOnBoard Encoder_2(SLOT2);

// Interruptfunktion für Encoder 1
void isr_process_encoder1(void) {
  if (digitalRead(Encoder_1.getPortB()) == 0)     Encoder_1.pulsePosMinus();
  else                                            Encoder_1.pulsePosPlus();
}

// Interruptfunktion für Encoder 2
void isr_process_encoder2(void) {
  if (digitalRead(Encoder_2.getPortB()) == 0)     Encoder_2.pulsePosMinus();
  else                                            Encoder_2.pulsePosPlus();
}

int lastOutputMillis;
int targetSpeedRPM;
char buf[4];
int buffIndex;

void setup() {
  // Festlegen der Interruptfunktionen für das Messen/Zählen der Bewegung
  attachInterrupt(Encoder_1.getIntNum(), isr_process_encoder1, RISING);
  attachInterrupt(Encoder_2.getIntNum(), isr_process_encoder2, RISING);
  Serial.begin(115200);
  
  // Interne Timer der PWM auf 8KHz programmieren
  TCCR1A = _BV(WGM10);
  TCCR1B = _BV(CS11) | _BV(WGM12);

  TCCR2A = _BV(WGM21) | _BV(WGM20);
  TCCR2B = _BV(CS21);

  // die PID Reglerparameter einstellen, eigentlich nur den P-Teil des Reglers, denn der Integral und Differentialteil
  // funktionieren gar nicht erst 
  // (der Differentialteil ist bei einem Motor auch nicht sinnvoll, außer der Fahrtwiderstand
  // ändert sich abrupt... was aber eher unwahrscheinlich ist)
  Encoder_1.setSpeedPid(0.18, 0, 0);
  Encoder_2.setSpeedPid(0.18, 0, 0);

  // Variable für gelegentliche Ausgaben auf die serielle Schnittstelle
  lastOutputMillis = millis();
  targetSpeedRPM = 0;
  buffIndex = 0;
}

void loop() {
  // von der seriellen Schnittstelle lesen

  // eine maximal 3-stellige Zahl von der seriellen Schnittstelle lesen
  char lastChar;
  while (Serial.available() && buffIndex<4) {
    lastChar = Serial.read();
    // Falls noch weniger < 3 Zeichen und kein Zeilenende, Zeichen in Puffer schieben
    if (lastChar != '\n' && buffIndex != 3)
      buf[buffIndex++] = lastChar;
    else {
      // Zeichenkette in Puffer mit \0 beenden
      buf[buffIndex] = '\0';
      // Text in Zahl umwandeln
      targetSpeedRPM = atoi(buf);
      Serial.print("targetspeed = ");
      Serial.println(targetSpeedRPM);
      // Motorzielgeschwindigkeiten anpassen
      Encoder_1.runSpeed(-targetSpeedRPM);
      Encoder_2.runSpeed(targetSpeedRPM);
      buffIndex = 0;
    }
  }

  // In der loop() Funktion wird die Geschwindigkeit im Motor geregelt
  Encoder_1.loop();
  Encoder_2.loop();

  // alle 100 Millisekunden die Geschwindigkeit und Duty Cycle ausgeben
  if (millis() > lastOutputMillis + 100) {
    lastOutputMillis = millis();
    Serial.print("TargetSpeed:");
    Serial.print(targetSpeedRPM);
    Serial.print(",Speed1:");
    Serial.print(-Encoder_1.getCurrentSpeed());
    Serial.print(",PWM1:");
    Serial.print(-Encoder_1.getCurPwm());
    Serial.print(",Speed2:");
    Serial.print(Encoder_2.getCurrentSpeed());
    Serial.print(",PWM2:");
    Serial.println(Encoder_2.getCurPwm());
  }
}
```

In diesem Programm wird die geforderte Geschwindigkeit über die serielle Schnittstelle gelesen. Im SerialPlotter oder SerialMonitor kann man dazu eine 3-stellige Zahl eingeben. In jedem Schleifendurchlauf wird ein neues Zeichen von der seriellen Schnittstelle in einen Pufferspeicher gelesen, bis entweder ein Zeilenendzeichen (`\n`) folgt, oder das 4. Zeichen gelesen wurde. Dann wird der Text im  Pufferspeicher mit einem `\0` beendet und in eine Zahl gewandelt. Diese wird dann als Soll-Drehzahl übergeben (in der etwas unzutreffenden Funktion `runSpeed()`. Der Controller berechnet in der stets aufgerufenen `MeEncoderOnBoard::loop()`-Funktion den Regelfehler und passt den Duty Cycle (PWM) entsprechend an.

.Geregelte Motorleistung, getestet mit 4 Geschwindigkeiten (50, 100, 200, und Max. 340 rpm) sieht das so aus (ein Motor wieder mit Kette, der andere frei drehend):
image::auriga_encoder_speed_8kHz_RPM_controlled.png[]

Man sieht sehr schön, dass der Regler es eigentlich ganz gut schafft, bei beiden Motoren die geforderten Drehzahlen zu erreichen. Nur in der höchsten Stufe schafft es der Motor mit Kette nicht ganz die geforderte Drehzahl zu erreichen, denn er läuft schon konstant am obersten Limit (Duty Cycle 100%). Aufgrund der unterschiedlichen Reibwiderstände muss der Motor mit der Ketten (hellblau) stets einen deutlich höheren Duty Cycle fahren, um die Drehzahl zu erreichen.

[WARNING]
====
Die Implementierung des PID-Reglers ist unvollständig - eigentlich funktioniert nur der P-Teil des Reglers. Aber im Rahmen der möglichen Genauigkeiten passt das soweit.
====

## Programmierung des Rangers OHNE Makeblock-Bibliothek mit robuster Entfernungsmessung

Die Makeblock-Bibliothek und die Klasse `MeEncoderOnBoard` sind für präzise Steuerungen des Roboters nicht wirklich optimal, da das Regelsignal (Geschwindigkeit) durch das stets automatische Rücksetzen beim Auswerten ziemlich zappelt. Wenn man mehr Kontrolle über die Regelung haben möchte, kommt man an einer eigenen Behandlung der Encoder nicht drum herum. 

Hier ist ein Beispielcode, der fast das Gleiche macht, wie das Listing in <<sec:motor:encodertest>>.

```c++
// Testprogramm für die Ansteuerung der DC Motoren OHNE Makeblock Bibliothek

#define PWMA  11 // Motor A/Links
#define DIRA1 49
#define DIRA2 48
#define PWMB  10 // Motor B/Rights
#define DIRB1 47
#define DIRB2 46

#define ENCODER1_PIN1 19
#define ENCODER1_PIN2 42

#define ENCODER2_PIN1 18
#define ENCODER2_PIN2 43

#define RPM2PWMFACTOR 1.33
#define PULSESPERREVOLUTION 353.403

class Encoder {
public:
  Encoder(int pin1, int pin2) : m_pin1(pin1), m_pin2(pin2), m_pulses(0) { m_speedMillis = millis(); }

  float rpmPerMinute() {
    unsigned long deltaT = millis() - m_speedMillis;
    float pulsePerSecond = m_pulses*1000.0/deltaT;
    float rpmPerMinute = pulsePerSecond*60/PULSESPERREVOLUTION; // 353.403 Pulse pro Umdrehung
    return rpmPerMinute;
  }

  void resetPulses() {
    m_speedMillis = millis();
    m_pulses = 0;
  }

  int m_pin1;
  int m_pin2;
  long m_pulses;
  unsigned long m_speedMillis;
};

Encoder encoder1(ENCODER1_PIN1, ENCODER1_PIN2);
Encoder encoder2(ENCODER2_PIN1, ENCODER2_PIN2);

unsigned long startMillis;
unsigned long lastOutputMillis;
int targetPWM = 0;
char buf[4];
int buffIndex=0;


// Für präzise Messung der Geschwindigkeit (und damit Position) der Motoren 
// werden die Pulse via Interruptfunktionen hoch/runtergezählt.
// Jedes Mal, wenn der Interrupt-Pin eines Encoders von LOW auf HIGH wechselt (RISING),
// wird die jeweilige isr_process_encoderX() Funktion aufgerufen und zählt einen 
// Pulse hoch.

// Interruptfunktion für Encoder 1
void isr_process_encoder1(void) {
  if (digitalRead(encoder1.m_pin2) == 0)
    --encoder1.m_pulses;
  else
    ++encoder1.m_pulses;
}

// Interruptfunktion für Encoder 2
void isr_process_encoder2(void) {
  if (digitalRead(encoder2.m_pin2) == 0)
    --encoder2.m_pulses;
  else
    ++encoder2.m_pulses;
}


void setup() {
  // PWM und Richtungspins setzen
  pinMode(PWMA,  OUTPUT);  
  pinMode(DIRA1, OUTPUT); 
  pinMode(DIRA2, OUTPUT);
  pinMode(PWMB,  OUTPUT);  
  pinMode(DIRB1, OUTPUT); 
  pinMode(DIRB1, OUTPUT);

// WICHTIG: INPUTs mit PullUp Widerständen!
  pinMode(ENCODER1_PIN1, INPUT_PULLUP);   
  pinMode(ENCODER1_PIN1, INPUT_PULLUP); 
  pinMode(ENCODER2_PIN2, INPUT_PULLUP); 
  pinMode(ENCODER2_PIN2, INPUT_PULLUP); 

  // kurze Wartephase zur Initialisierung
  delay (20);

  // Festlegen der Interruptfunktionen für das Messen/Zählen der Bewegung
  attachInterrupt(digitalPinToInterrupt(ENCODER1_PIN1), isr_process_encoder1, RISING);
  attachInterrupt(digitalPinToInterrupt(ENCODER2_PIN1), isr_process_encoder2, RISING);
  Serial.begin(115200);
  
  // Interne Timer der PWM auf 8KHz programmieren
  TCCR1A = _BV(WGM10);
  TCCR1B = _BV(CS11) | _BV(WGM12);

  TCCR2A = _BV(WGM21) | _BV(WGM20);
  TCCR2B = _BV(CS21);

  // Variable für gelegentliche Ausgaben auf die serielle Schnittstelle
  lastOutputMillis = startMillis = millis();
}

void leftMotorSpeed(int targetPWM) {
  if (targetPWM >= 0) {
    analogWrite (PWMA,  targetPWM);
    digitalWrite(DIRA1, LOW);
    digitalWrite(DIRA2, HIGH);
  }
  else {
    analogWrite (PWMA,  -targetPWM);
    digitalWrite(DIRA1, HIGH);
    digitalWrite(DIRA2, LOW);
  }
}

void rightMotorSpeed(int targetPWM) {
  if (targetPWM >= 0) {
      analogWrite (PWMB,  targetPWM);
      digitalWrite(DIRB1, HIGH);
      digitalWrite(DIRB2, LOW);
  }
  else {
    analogWrite (PWMB,  -targetPWM);
    digitalWrite(DIRB1, LOW);
    digitalWrite(DIRB2, HIGH);
  }
}


void loop() {

  // eine maximal 3-stellige Zahl von der seriellen Schnittstelle lesen
  char lastChar;
  while (Serial.available() && buffIndex<4) {
    lastChar = Serial.read();
    // Falls noch weniger < 3 Zeichen und kein Zeilenende, Zeichen in Puffer schieben
    if (lastChar != '\n' && buffIndex != 3)
      buf[buffIndex++] = lastChar;
    else {
      // Zeichenkette in Puffer mit \0 beenden
      buf[buffIndex] = '\0';
      // Text in Zahl umwandeln
      targetPWM = atoi(buf);
      // auf Wertebereich -255 bis 255 begrenzen
      targetPWM = constrain(targetPWM, -255, 255);
      Serial.print("targetspeed = ");
      Serial.println(targetPWM);
      // Motorgeschwindigkeiten anpassen
      leftMotorSpeed(targetPWM);
      rightMotorSpeed(targetPWM);
      // den Counter zurücksetzen
      encoder1.resetPulses();
      encoder2.resetPulses();
      buffIndex = 0;
    }
  }

  // alle 100 Millisekunden die Geschwindigkeit ausgeben
  if (millis() > lastOutputMillis + 100) {
    lastOutputMillis = millis();
    // Ausgabe: Zeit [s]  \t Ziel-PWM \t Speed 1 [rpm] \t Speed 2 [rpm]
    Serial.print((millis() - startMillis)*0.001);
    Serial.print("\t");
    Serial.print(targetPWM);
    Serial.print("\t");
    Serial.print(-encoder1.rpmPerMinute());
    Serial.print("\t");
    Serial.println(encoder2.rpmPerMinute());
    // den Counter zurücksetzen
    encoder1.resetPulses();
    encoder2.resetPulses();
  }
}
```


Man kann im SerialMonitor or SerialPlotter nun die Sollleistung in PWM (-255..255) angeben.

