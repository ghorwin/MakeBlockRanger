:imagesdir: ../images

[[chap:motors]]
# Motorsteuerung

Jetzt wird endlich mal was bewegt. In diesem Teil geht es um die Programmierung der Motoren. 

Zunächst aber mal ein konstruktiver Tipp:

[TIP]
====
Die Ketten sind am Anfang ewas steif und durch die Lagerung und Verpackung etwas oval verformt. Wenn man den _Land Raider_ wie in der Konstruktionszeichnung zusammen baut, dann sitzen die Ketten _sehr_ straff und der Motor braucht jedes Mal viel Kraft, um aus der gekrümmten Kettenform rauszudrehen (was er bei etwas entleerten Batterien häufig nicht schafft). Bei der vorgegebenen Firmware führt das dann beim Steuern des _Land Raider_ häufig dazu, dass er mit einem Beepen stehen bleibt (warum die Firmware das macht und wie diese erkennt, dass der _Land Raider_ stecken bleibt, erklär ich unten).

Es gibt zwei Dinge, die man tun kann, um dieses Problem mit dem _Land Raider_ zu beheben:

- die Ketten "rund" dehnen, z.B. auf eine ausreichend dicke Blumevase schieben, bis sie leicht gespannt sind, und dann eine Weile in die Sonne stellen und ggfs. gelegentlich nachspannen
- die Motoren um eine Bohrung nach Innen befestigen, wie auf dem nachfolgenden Bild zu sehen ist

.Um ein Loch verschobene Motormontage zur Reduktion der Spannung in den Ketten
image::raider_motor_verschoben.jpg[]
====


## Allgemeines

Am Auriga-Board gibt es zwei Motorports. Jeder der DC-Motoren kann über einen PWM Pin (analoge Spannung für die Geschwindigkeit) und zwei digitalen Pins für die Drehichtung angesteuert werden. Die Ansteuerung kann einmal direkt über die Arduino-Pins erfolgen oder unter Verwendung der Makeblock Bibliothek (in der die Details gekapselt sind).

[NOTE]
====
Grundsätzlich bewegen sich die Motoren nur, wenn die Spannungsversorgung über die Batterien erfolgt (die Motoren brauchen eine Spannung > 7V, was der USB-Port ja nicht liefern kann). Das ist auch ganz praktisch, da man so den Robotor angesteckt an den Computer programmieren kann, ohne Angst zu haben, dass er vom Tisch rollt.
====

Die Motoren werden an die beiden Motorports angesteckt:

.Motor-Ports an der Auriga-Platine
image::auriga_motorports.png[]


[TIP]
====
Die DC-Motoren des _Ranger_ Baukastens sind intern mit einem Getriebe versehen. Die eigentlichen Elektromotoren drehen bei Maximalleistung  (d.h. vollen Batterien und ohne Kette) mit ungefähr 14000 Umdrehungen pro Minute (oder engl. _revolutions per minute - rpm_). Durch das Getriebe mit der Übersetzung 39,6 laut Datenblatt drehen sich die angesteckten Räder eher so mit 350 rpm. Bei einem Radius von ca. 22 mm (einschließlich Kette) und damit einem Abrollumfang von Pi*2*22mm sind das dann ca. 138 mm pro Umdrehung und stolze 350*138 mm = *48 m pro Minute* (oder *0,8 m/s* oder ca. *2,9 km/h*). Das wäre dann also die theoretische Maximalgeschwindigkeit.
====

Zunächst beschreibe ich mal die Programmierung der Motoren ohne die MakeBlock-Bibliotheksklassen. Dadurch versteht man besser, wie das alles zusammen funktioniert. Danach beschreibe ich in <<sec:motor:makeblocklib>> die Funktionalität der MakeBlock-Bibliotheksklasse zur Encoder-Motor-Programmierung.


## Direkte Ansteuerung mit Arduino-Hausmitteln

Aus dem Schaltplan kann man die Bezeichnung der digitalen Pins (D46...D49) und die PWM Pins (D10, D11) entnehmen:

.Schaltplanauszug für die Motoransteuerung
image::auriga_schaltplanauszug_motor.png[]

Die digitalen Pins werden nun wie folgt angesprochen:

- PWMA und PWMB erhalten Drehgeschwindigkeiten (PWM, im Bereich 0..255)
- DIRA1 und DIRA2 regeln die Richtung des Motors A (links) und müssen jeweils entgegengesetzt geschaltet sein
- DIRB1 und DIRB2 regeln die Richtung des Motors B (rechts) und müssen jeweils entgegengesetzt geschaltet sein

[NOTE]
====
DC-Motoren drehen sich in Abhängigkeit der anliegenden Gleichspannung schneller oder langsamer - je höher die Spannung, umso schneller. Der Arduino liefert die für die DC-Motoren benötigte Spannung aber nicht direkt, denn die Motoren brauchen eine Spannung zwischen 7..8.2 V. Die Motoren werden vom Batteriepack mit Spannung versorgt.

Der Arduino generiert aber eine quasi-Analoge Spannung durch Verwendung von PWMs. Hierbei wird in einem bestimmten Takt (der z.B. 1ms dauert) die Spannung kurz angeschaltet und danach wieder kurz ausgeschaltet. Je nachdem wie lange die Spannung  im Verhältnis zur Gesamtzeit angeschaltet ist, umso höher ist die generierte Quasi-Gleichspannung. Das Verhältnis von "An-Zeit" zur gesamten Taktzeit nennt man _Duty-Cycle_. Bei 100% wird also die Nominalspannung des Arduino (5V) ausgegeben. 

Diese am Pin anliegende Spannung wird im DC-Motor genutzt, um den Strom vom Batteriepack zum eigentlichen DC-Motor zu leiten (Internetsuche zum Thema "DC-Motoren und MOSFET"). Je höher die Taktfrequenz des PWM-Signals, umso gleichmäßiger baut sich das Magnetfeld auf/ab und der Motor läuft ruhiger. Dies ist insbesondere bei geringeren Duty-Cycles wichtig (siehe auch <<sec:motor_dutyCycle>>).
====


### Beispielprogramm - Beschleunigen und Abbremsen

```c++
// Testprogramm für die Ansteuerung der Motoren via Arduino Pins

#define PWMA  11 // Motor A/Links
#define DIRA1 49
#define DIRA2 48
#define PWMB  10 // Motor B/Rights
#define DIRB1 47
#define DIRB2 46

void setup() {
  // PWM und Richtungspins setzen
  pinMode(PWMA,  OUTPUT);  
  pinMode(DIRA1, OUTPUT); 
  pinMode(DIRA2, OUTPUT);
  pinMode(PWMB,  OUTPUT);  
  pinMode(DIRB1, OUTPUT); 
  pinMode(DIRB1, OUTPUT);

  // kurze Wartephase zur Initialisierung
  delay(20);
}

void setLeftMotorSpeed(int speed) {
  if (speed >= 0) {
    analogWrite (PWMA,  speed);
    digitalWrite(DIRA1, LOW);
    digitalWrite(DIRA2, HIGH);
  }
  else {
    analogWrite (PWMA,  -speed);
    digitalWrite(DIRA1, HIGH);
    digitalWrite(DIRA2, LOW);
  }
}

void setRightMotorSpeed(int speed) {
  if (speed >= 0) {
      analogWrite (PWMB,  speed);
      digitalWrite(DIRB1, HIGH);
      digitalWrite(DIRB2, LOW);
  }
  else {
    analogWrite (PWMB,  -speed);
    digitalWrite(DIRB1, LOW);
    digitalWrite(DIRB2, HIGH);
  }
}


// Maximalgeschwindigkeit
int maxSpeed = 255;
int speed = 0;
bool upward = true;

void loop() {

  // Steuerungsprogramm: 
  // - Vorwärtsfahren und Geschwindigkeit in jeder Schleife 
  //   erhöhen bis Maximalgeschwindikeit erreicht ist
  // - dann langsamer werden und schließlich Rückwärtsfahren 
  //   bis maximale Rückwärtsgeschwindigkeit erreicht ist
  // - dann wieder umschalten auf vorwärts fahren
  if (upward)
    speed += 5;
  else
    speed -= 5;
  if (speed > maxSpeed) {
    upward = false;
    speed = maxSpeed;
  }
  if (speed < -maxSpeed) {
    upward = true;
    speed = -maxSpeed;
  }

  // Motorgeschwindigkeiten anpassen
  setLeftMotorSpeed(speed);
  setRightMotorSpeed(speed);

  delay(50);
}
```

Hier fährt der Roboter einfach nur vor und zurück und beschleunigt/bremst dabei.

### Beispielprogramm - Konstante Fahrt vorwärts

Man kann das Programm auch vereinfachen und den Roboter einfach mit konstanter Geschwindigkeit geradeaus fahren lassen. Dabei werden beide Motoren mit dem gleichen PWM-Signal (gleicher Leistung) angesteuert.


```c++
// Testprogramm für die Ansteuerung der Motoren via Arduino Pins

#define PWMA  11 // Motor A/Links
#define DIRA1 49
#define DIRA2 48
#define PWMB  10 // Motor B/Rights
#define DIRB1 47
#define DIRB2 46

void setup() {
  // PWM und Richtungspins setzen
  pinMode(PWMA,  OUTPUT);  
  pinMode(DIRA1, OUTPUT); 
  pinMode(DIRA2, OUTPUT);
  pinMode(PWMB,  OUTPUT);  
  pinMode(DIRB1, OUTPUT); 
  pinMode(DIRB1, OUTPUT);

  // kurze Wartephase zur Initialisierung
  delay (20);

  // links vorwärts, 100 PWM
  analogWrite (PWMA,  100);
  digitalWrite(DIRA1, LOW);
  digitalWrite(DIRA2, HIGH);

  // rechts rückwärts, 100 PWM
  analogWrite (PWMB,  100);
  digitalWrite(DIRB1, HIGH);
  digitalWrite(DIRB2, LOW);
}

// in der Hauptschleife machen wir nix
void loop() {
}
```

Nun _sollte_ der Raider schön geradeaus fahren... macht er aber gelegentlich nicht. Warum? Weil je nach Steifigkeit der Kette oder Untergrund oder anderen Reibungsfaktoren sich die Motoren trotz gleichem PWM-Signal nicht gleich schnell drehen. Um das zu beheben und wirklich präzise zu navigieren, müsste man die Motorleistung so anpassen, dass die geforderte Drehzahl erreicht wird. Dafür müsste man aber herausfinden, wie schnell sich die Motoren _tatsächlich_ drehen. Und genau das kann man bei den Encoder-Motoren machen.

## Motordrehgeschwindigkeit mittels Encoder ermitteln

Der Encoder im Motor ist ein Drehimpulsgeber/Tacho und liefert elektrische Impulse, die man zählen kann. Damit kann man die Geschwindigkeit und zurückgelegte Entfernung bestimmen. Zum Einstieg in das Thema _Encoder_ kann ich das https://www.youtube.com/watch?v=-SFIOm5mt4U[Encoder Tutorial Arduino (deutsch, für Anfänger)] empfehlen.

Der Encoder eines Motors ist am Arduino an jeweils zwei digitalen Pins angeklemmt. Um einen Drehimpuls zu erfassen, muss man den einen Pin überwachen und feststellen, wann dieser von 0 auf 1 wechselt. Damit man das präzise machen kann, ohne dass anderen Aufgaben im Mikroprozessor die Messung beeinträchtigen, verwendet man Interruptfunktionen. Dazu registriert man für jeden Motor eine Interruptfunktion, die immer dann aufgerufen wird, wenn am entsprechenden Pin ein RISING Signal anliegt (also der Eingangspin von LOW auf HIGH wechselt). In der Interruptfuktion wird dann ein Zähler hochgezählt, der dann für die Berechnung der Momentangeschwindigkeit und Bewegung verwendet wird. 


### Beispielprogramm - Variable Motordrehzahl und Geschwindigkeit via Encoder auslesen

Nun folgt ein etwas längeres Beispielprogramm, welches aber unten detailliert erklärt wird.

```c++
// Testprogramm für die Ansteuerung der Encoder-Motoren via Arduino Pins

// Motor A/Links
#define PWMA  11 
#define DIRA1 49
#define DIRA2 48

#define ENCODER1_PIN1 19  // Interrupt-Pin für Encoder 1
#define ENCODER1_PIN2 42

// Motor B/Rechts

#define PWMB  10 
#define DIRB1 47
#define DIRB2 46

#define ENCODER2_PIN1 18  // Interrupt-Pin für Encoder 2
#define ENCODER2_PIN2 43

#define PULSESPERREVOLUTION 353.403   // Anzahl der Pulse pro Umdrehung

int targetSpeedPWM;                   // Ziel-PWM für beide Motoren
unsigned long startMillis;            // Millis am Programmstart
unsigned long pulseCountStartMillis;  // Millis beim Start der Pulszählung
unsigned long lastOutputMillis;       // Millis bei der letzten Ausgabe

long encoder1Pulses;                  // gezählte Pulse des Encoders 1 (können negativ werden)
long encoder2Pulses;                  // gezählte Pulse des Encoders 2 (können negativ werden)

// Für präzise Messung der Geschwindigkeit (und damit Position) der Motoren 
// werden die Pulse via Interruptfunktionen hoch/runtergezählt.
// Jedes Mal, wenn der Interrupt-Pin eines Encoders von LOW auf HIGH wechselt (RISING),
// wird die jeweilige isr_encoderX() Funktion aufgerufen und zählt einen Puls hoch.

// Interruptfunktion für Encoder 1
void isr_encoder1(void) {
  if (digitalRead(ENCODER1_PIN2) == 0)
    ++encoder1Pulses;
  else
    --encoder1Pulses;
}

// Interruptfunktion für Encoder 2
void isr_encoder2(void) {
  if (digitalRead(ENCODER2_PIN2) == 0)
    --encoder2Pulses;   // ACHTUNG: Drehrichtung des 2. Motors ist andersherum
  else
    ++encoder2Pulses;
}

// Umrechung von Pulsen zu Umdrehungen pro Minute (RPM)
float rpmFromPulses(long pulses) {
  unsigned long deltaT = millis() - pulseCountStartMillis; // Zeit seit Beginn der Zählung
  float pulsePerSecond = pulses*1000.0/deltaT;
  float rpm = pulsePerSecond*60/PULSESPERREVOLUTION; // 353.403 Pulse pro Umdrehung
  return rpm;
}

// setzt Geschwindigkeit für linken Motor (-255...255)
void setLeftMotorSpeed(int targetPWM) {
  if (targetPWM >= 0) {
    analogWrite (PWMA,  targetPWM);
    digitalWrite(DIRA1, LOW);
    digitalWrite(DIRA2, HIGH);
  }
  else {
    analogWrite (PWMA,  -targetPWM);
    digitalWrite(DIRA1, HIGH);
    digitalWrite(DIRA2, LOW);
  }
}

// setzt Geschwindigkeit für rechten Motor (-255...255)
void setRightMotorSpeed(int targetPWM) {
  if (targetPWM >= 0) {
      analogWrite (PWMB,  targetPWM);
      digitalWrite(DIRB1, HIGH);
      digitalWrite(DIRB2, LOW);
  }
  else {
    analogWrite (PWMB,  -targetPWM);
    digitalWrite(DIRB1, LOW);
    digitalWrite(DIRB2, HIGH);
  }
}


void setup() {
  // PWM und Richtungspins setzen
  pinMode(PWMA,  OUTPUT);  
  pinMode(DIRA1, OUTPUT); 
  pinMode(DIRA2, OUTPUT);
  pinMode(PWMB,  OUTPUT);  
  pinMode(DIRB1, OUTPUT); 
  pinMode(DIRB1, OUTPUT);

  // WICHTIG: INPUTs mit PullUp Widerständen!
  pinMode(ENCODER1_PIN1, INPUT_PULLUP);   
  pinMode(ENCODER1_PIN2, INPUT_PULLUP); 
  pinMode(ENCODER2_PIN1, INPUT_PULLUP); 
  pinMode(ENCODER2_PIN2, INPUT_PULLUP); 

  // kurze Wartephase zur Initialisierung
  delay (20);

  // Festlegen der Interruptfunktionen für das Messen/Zählen der Bewegung
  attachInterrupt(digitalPinToInterrupt(ENCODER1_PIN1), isr_encoder1, RISING);
  attachInterrupt(digitalPinToInterrupt(ENCODER2_PIN1), isr_encoder2, RISING);

  // Variable für gelegentliche Ausgaben auf die serielle Schnittstelle
  lastOutputMillis = startMillis = pulseCountStartMillis = millis();

  encoder1Pulses = 0;
  encoder2Pulses = 0;

  // Serielle Kommunikation konfigurieren
  Serial.begin(115200);
}


// in der Hauptschleife verändern wir je nach Eingabe der Geschwindigkeit 
// das Tempo und geben regelmäßig Zielwert und tatsächliche Geschwindigkeit aus
void loop() {
  // von der seriellen Schnittstelle lesen
  if (Serial.available()) {
    // Zeichen 0...6 werden gelesen
    char a = Serial.read();
    switch(a) {
      case '0': targetSpeedPWM =    0; break;  // stop
      case '1': targetSpeedPWM =   20; break;  // slow forward
      case '2': targetSpeedPWM =  200; break;  // fast forward
      case '3': targetSpeedPWM =  255; break;  // fastest forward
      case '4': targetSpeedPWM = -100; break;  // slow backward
      case '5': targetSpeedPWM = -200; break;  // fast backward
      case '6': targetSpeedPWM = -255; break;  // fastest backward
    }
    setLeftMotorSpeed(targetSpeedPWM);
    setRightMotorSpeed(targetSpeedPWM);
  }

  // alle 100 Millisekunden die Geschwindigkeit ausgeben
  if (millis() > lastOutputMillis + 500) {
    lastOutputMillis = millis();
    // Ausgabe: Zeit [s]  \t Ziel-PWM \t Speed 1 [rpm] \t Speed 2 [rpm]
    Serial.print((millis() - startMillis)*0.001);
    Serial.print("\t");
    Serial.print(targetSpeedPWM);
    Serial.print("\t");
    Serial.print(rpmFromPulses(encoder1Pulses));
    Serial.print("\t");
    Serial.println(rpmFromPulses(encoder2Pulses));
    // die Counter zurücksetzen
    encoder1Pulses = 0;
    encoder2Pulses = 0;
    pulseCountStartMillis = millis();
  }
}
```

Dieses Programm hat viele Bestandeile, die man aber am Besten nach und nach erklärt.

### Interruptfunktionen für Encoder-Pins und Geschwindigkeitsberechnung

Für die Erfassung der Geschwindigkeit müssen wir die Encoder-Pins (2 für jeden Motor) auslesen. Hierbei ist wichtig, diese als INPUT-Pins mit integriertem Pullup-Widerstand zu konfigurieren. Der entsprechende Block in der `setup()`-Funktion ist:

```c
// WICHTIG: INPUTs mit PullUp Widerständen!
pinMode(ENCODER1_PIN1, INPUT_PULLUP);   
pinMode(ENCODER1_PIN2, INPUT_PULLUP); 
pinMode(ENCODER2_PIN1, INPUT_PULLUP); 
pinMode(ENCODER2_PIN2, INPUT_PULLUP); 
```

Wie schon oben erwähnt, werden Interruptfunktionen mit den Interrupt-Pins der Encoder verknüpft, wobei auf den Wechsel des Pegels von LOW auf HIGH reagiert wird (`RISING`). Das passiert in der `setup()` Funktion:

```c++
  // Festlegen der Interruptfunktionen für das Messen/Zählen der Bewegung
  attachInterrupt(digitalPinToInterrupt(ENCODER1_PIN1), isr_encoder1, RISING);
  attachInterrupt(digitalPinToInterrupt(ENCODER2_PIN1), isr_encoder2, RISING);
```

[NOTE]
====
Der Funktionspräfix `isr` steht für _Interrupt Service Routine_.
====

Auf dem Mega 2560 gibt es mehrere Interrupt-fähige Pins, unter anderem Pin 18 und 19, welche mit dem Encoder verbunden sind (siehe auch Pinout-Diagramm, M1 und M2, wo D19/INT1 und D18/INT3 steht). Diesen Pins wird im Mega 2560 jeweils eine Interrupt-Nummer zugewiesen. Mit der Funktion `digitalPinToInterrupt()` erhält man die zu einem Interrupt-fähigen Pin zugehörige Interrupt-Nummer:

```c
int interruptNr1 = digitalPinToInterrupt(19);  // -> interrupt 4
int interruptNr2 = digitalPinToInterrupt(18);  // -> interrupt 5
```

Siehe auch Erklärung zu den Interrupt-Nummern in https://docs.arduino.cc/language-reference/funktionen/external-interrupts/attachInterrupt/


Wie bei Encodern üblich, wird die Flanke des einen Pins überwacht und dann durch den (etwas versetzten) Pegel des zweiten Pins die Drehrichtung bestimmt. Dies geschicht in der Interrupt-Funktion:

```c
// Interruptfunktion für Encoder 1
void isr_encoder1(void) {
  if (digitalRead(ENCODER1_PIN2) == 0)
    ++encoder1Pulses;
  else
    --encoder1Pulses;
}
```

In den Interrupt-Funktionen wird jetzt bei jedem Aufruf ein Puls hoch oder runter gezählt. Da sich der Motor 2 anders herum dreht, wird entsprechend invertiert gezählt (sodass positive Pulszahlen einer Vorwärtsfahrt entsprechen).

Die Funktion `rpmFromPulses()` rechnet nun die bislang gezählten Pulse in eine Drehzahl um. Die seit Beginn der Zählung verstrichene Zeit wird berechnet und dann wird auf Pulse pro Minute hochgerechnet. Die Encoder geben bei einer Umdrehung 353.403 Pulse ab.

[NOTE]
====
Die Encodermotoren drehen sich eigentlich bei Maximalgeschwindigkeit mit ca. 14000 Umdrehungen pro Minute (rpm). Das eingebaute Getriebe hat laut Spezifikation ein Verhältnis von 39.6, sodass sich das angesteckte Rad selbst eher mit 350 rpm dreht. In der Makeblock-Bibliothek ist jedoch ein Getriebeverhältnis von 39.267 angegeben, also hab ich diesen Wert verwendet. 

Bei jeder Umdrehung des Motors werden 9 Pulse abgegeben. Damit ergeben sich bei einer Umdrehung des Rades die 9*39.267=353.403 Pulse.
====

Nach Berechnung der Geschwindigkeit wird ein neues Zählintervall gestartet, indem der Startzeitpunkt aktualisiert wird und die Pulszählvariablen auf 0 gesetzt werden:

```c
// die Counter zurücksetzen
encoder1Pulses = 0;
encoder2Pulses = 0;
pulseCountStartMillis = millis();
```

## Steuerung der Geschwindigkeit mittels Serial Plotter/Serial Monitor

Damit man die Rotationsgeschwindigkeit in Abhängigkeit des gesetzten PWM-Duty-Cycles testen kann, wird im Programm von der seriellen Schnittstelle gelesen. Diese serielle Schnittstelle dient eigentlich dazu, Daten vom Roboter an die Arduino-IDE zu geben, z.B. den SerialPlotter zur Anzeige (wie das schon in <<chap:sensors>> mit den Sensoren gemacht wurde).

Mann kann aber genauso gut auch Text zurück an den Roboter senden. Dazu tippt man einach im SerialMonitor oder SerialPlotter den Text ein und bestätigt mit Enter. In diesem Programmbeispiel werden die Zeichen 0 bis 6 akzeptiert und je nach Zeichen wird eine andere PWM-Leistung eingestellt. In einem späteren Beispiel wird diese Kommunikation nochmal etwas erweitert.

In diesem Testbeispiel kann man den Robotor begrenzt steuern:

- Nach Start des Programms den SerialMonitor der Arduino-IDE aufrufen
- in die Zeile "Nachricht" des SerialMonitor eine Zahl 0...6 eintippen und mit Enter abschicken
- das Programm wertet diese Zahl aus und legt die Drehgeschwindigkeit der Motoren fest

Alternativ kann man das auch im SerialPlotter machen.


## Testlauf und Geschwindigkeitsmessung mit und ohne Kette

Wenn man jetzt das Programm mal und die Geschwindigkeiten plottet erhält man ein interessantes Bild:

.Ausgabe der Motorgeschwindigkeiten (in Umdrehungen pro Minute) mit einem Motor mit Kette und einem freidrehenden Motor
image::auriga_encoder_speed_original_PWM_timer_one_belt.png[]

Da die Kette schwingt und auch nicht gleichmäßig steif ist, führt dies zu ungleichmäßigen Drehbewegungen des Motors. Um diesen Einfluss zu sehen, habe ich an einen Motor die Kette angebaut und am anderen Motor nicht. Klar erkennbar im Diagramm ist auch die Bremswirkung der Kette insgesamt - der Motor mit Kette dreht langsamer.

Außerdem scheint die Drehzahl nicht proportional zum PWM-Duty-Cycle zu steigen (vor allem bei PWM 100). Dies liegt daran, dass der generierte PWM-Takt zu langsam ist. Um dies zu beheben, kann der PWM-Timer des Arduino umprogrammiert werden, sodass er mit 8kHz läuft. 

[[sec:motor_dutyCycle]]
## Höhere PWM-Taktfrequenz für einen ruhigeren Motorlauf bei geringen Duty-Cycles

Die Drehzahl von DC-Motoren ändert sich in Abhängigkeit der angelegten Gleichstromspannung. Da der Arduino selbst keine analoge Spannung ausgeben kann, wird diese über Pulsweitenmodulation (PWM) erzeugt. Dafür wird an einem digitalen Ausgang der Spannungspegel für eine bestimmte Zeit hoch gezogen und dann wieder für eine bestimmte Zeit abgeschaltet. Die Summe aus der An- und Auszeit ist der PWM-Takt. 
Je nachdem, wie lange die Spannung anliegt im Verhältnis zur Taktlänge, umso höher ist die wirksame (simulierte) Analogspannung am Ausgangspin. Wenn man hier noch einen Kondensator anbaut, der die Spannungsstufen dämpft, erhält man tatsächlich eine hinreichend gleichmäßige analoge Gleichspannung.

[NOTE]
====
Das Verhältnis von An-Zeit zu Taktlänge nennt man _Duty Cycle_. Die Anzahl von PWM-Takten pro Sekunde ist der _PWM-Takt_.
====

Wenn man eine solche PWM-generierte Gleichspannung an einen Motor anschließt, so wird dadurch das Magnetfeld im Motor sehr häufig auf und abgebaut, und zwar proportional zum gewählten PWM-Takt.  Und gerade bei niedrigen Duty Cycles führt dies mitunter zu einem etwas unrunden Motorlauf. Wenn man jedoch den PWM-Takt erhöht, d.h. die PWM-Taktlänge reduziert, so führt dies zu einem viel häufigeren Auf- und Abbau des Magnetfelds im Motor und damit zu einem gleichmäßigerem Motorlauf. Interessanterweise kann man den PWM-Takt  umprogrammieren.

Der MEGA 2560 Prozessor hat intern Timer, die auch für die Generierung von PWM-Duty-Cycles verwendet werden. Für die Pins 11 und 12 ist der Timer 1 zuständig (16 bit). Und für Pins 9 und 10 der Timer 2. Für diese Pins ist die Standardfrequenz 490 Hz.

Diese Timer-Frequenzen kann man umprogrammieren (siehe auch Erklärung in https://wolles-elektronikkiste.de/interrupts-teil-3-timer-interrupts#calculations[Interrupts – Teil 3: Timer Interrupts] und beispielsweise die konkret verwendete Bitmaske für Timer 1 auf 
https://dbuezas.github.io/arduino-web-timers/#mcu=ATMEGA328P&timer=1&timerMode=FPWM&clockPrescalerOrSource=8[Arduino Web Timers]). 

Der relevante Code-Block ist:

```c
// Interne Timer für PWM auf 8KHz programmieren (Mode 5 - Fast PWM, 8-Bit, Prescaler 8)
// Timer 1
TCCR1A = _BV(WGM10);
TCCR1B = _BV(CS11) | _BV(WGM12);

// Timer 2
TCCR2A = _BV(WGM21) | _BV(WGM20);
TCCR2B = _BV(CS21);
```

`_BV(X)` steht für einen Bitshift `1 << X`.

Dadurch erhöht sich die PWM-Taktfrequenz und der Motor läuft gleichmäßiger und bei niedrigen Duty-Cycles auch schneller.

Erneut getestet sieht die Geschwindigkeitsausgabe deutlich besser aus, d.h. auch die durch die Encoder erfasste Drehzahl ist auch bei kleineren Drehzahlen hinreichend proportional zur Sollgeschwindigkeit (in PWM), was man gut am freilaufenden Motor sehen kann.

.Ausgabe der Motorgeschwindigkeiten (in Umdrehungen pro Minute) mit umprogrammierten PWM-Timern und jeweils einem Motor mit Kette und einem freidrehenden Motor
image::auriga_encoder_speed_8kHz_PWM_timer_one_belt.png[]

Beim freilaufenden Motor wird die festgelegte Soll-Geschwindigkeit offenbar gut erreicht. Beim Motor mit Kette bleibt die Sollgeschwindigkeit etwas hinter der festgelegten Geschwindigkeit, was ja an der Bremswirkung liegt.

Im Beispiel oben wurde die Geschwindigkeit in PWM angegeben und die Drehzahl vom Encoder in Umdrehungen pro Minute (RPM) abgelesen. Die Verhältnisse sind

  132/100 = 1.32
  266/200 = 1.33
  339/255 = 1.33
  
Damit _könnte_ man also die geforderte Drehzahl im Bereich -340...340 mit der Formel einstellen:

  pwm = rpm/1.33

und so hoffen, dass sich der Ranger genau mit der angegebenen Geschwindigkeit fortbewegt.

*ABER:* Die Drehzahl eines unbelasteten Rades alleine sagt ja noch nichts über die Fahrgeschwindigkeit des Roboters aus. Denn diese hängt natürlich auch noch vom Batterieladezustand ab und vom Anstieg und von der Reibung der Kette und und und... Deshalb ist es sinnvoller, die Leistung anhand der geforderten Geschwindigkeit einzuregeln. Doch dazu später mehr.

[TIP]
====
Wenn man den Land Raider mit aufgeladenen Batterien und Maximalgeschwindigkeit fahren lässt, so schafft er bei vollen Batterien auf gerader Fläche immerhin **0,56 m/s** bzw. **2 km/h**. Das ist zwar langsamer als die oben ausgerechnete Maximalgeschwindigkeit, reicht locker, um durch eine Legostadt zu düsen und spektakuläre Videos aus der Legomännelperspektive zu filmen.
====

[NOTE]
====
Als ich das Testprogramm gerade nochmal mit frisch aufgeladenen Akkus getestet habe, sind bei PWM 255 durchaus Drehzahlen bis 388 RPM angezeigt wurden. Die Akkuspannung hat also einen erheblichen Einfluss darauf, was an Drehzahlen und Geschwindigkeiten so möglich ist.
====




[[sec:motor:makeblocklib]]
## Ansteuerung über Encoder-Klasse der Bibliothek

Die MakeBlock-Bibliothek bietet zahlreiche Klassen für die Programmierung von Encoder-Motoren. Für die mitgelieferten DC-Motoren wird die Klasse `MeEncoderOnBoard` verwendet. Diese übernimmt, wie der Name schon sagt, die Programmierung der auf dem Board befindlichen Motorsteuerung. Diese Klasse erlaubt sowohl die direkte Ansteuerung der Motoren durch Setzen der Leistung via PWM, als auch die geregelte Steuerung.


[[sec:motor:encodertest]]
### Beispielprogramm - Ansteuerung mittels Bibliotheksfunktion

Zunächst soll das obige Testbeispiel hier nochmal unter Verwendung der Makeblock-Bibliothek umgesetzt werden.

.Testprogramm für die Motorsteuerung und Messung der Geschwindigkeit mittels der Encoder-Klasse in der Makeblock-Bibliothek
```c++
// Testprogramm für die Ansteuerung der DC Motoren via Makeblock-Bibliothek
#include <MeAuriga.h>

MeEncoderOnBoard Encoder_1(SLOT1);
MeEncoderOnBoard Encoder_2(SLOT2);

// Für präzise Messung der Geschwindigkeit (und damit Position) der Motoren 
// werden die Pulse via Interruptfunktionen hoch/runtergezählt.
// Jedes Mal, wenn der Interrupt-Pin eines Encoders von LOW auf HIGH wechselt (RISING),
// wird die jeweilige isr_process_encoderX() Funktion aufgerufen und zählt einen 
// Pulse hoch.

// Interruptfunktion für Encoder 1
void isr_process_encoder1(void) {
  if (digitalRead(Encoder_1.getPortB()) == 0)     Encoder_1.pulsePosMinus();
  else                                            Encoder_1.pulsePosPlus();
}

// Interruptfunktion für Encoder 2
void isr_process_encoder2(void) {
  if (digitalRead(Encoder_2.getPortB()) == 0)     Encoder_2.pulsePosMinus();
  else                                            Encoder_2.pulsePosPlus();
}


int lastOutputMillis;
int targetSpeedPWM;

void setup() {
  // Festlegen der Interruptfunktionen für das Messen/Zählen der Bewegung
  attachInterrupt(Encoder_1.getIntNum(), isr_process_encoder1, RISING);
  attachInterrupt(Encoder_2.getIntNum(), isr_process_encoder2, RISING);
  Serial.begin(115200);
  
  // Interne Timer der PWM auf 8KHz programmieren
  TCCR1A = _BV(WGM10);
  TCCR1B = _BV(CS11) | _BV(WGM12);

  TCCR2A = _BV(WGM21) | _BV(WGM20);
  TCCR2B = _BV(CS21);

  // Variable für gelegentliche Ausgaben auf die serielle Schnittstelle
  lastOutputMillis = millis();
  targetSpeedPWM = 0;
}

void loop() {
  // von der seriellen Schnittstelle lesen
  if (Serial.available()) {
    // Zeichen 0...6 werden gelesen
    char a = Serial.read();
    switch(a) {
      case '0': targetSpeedPWM =    0; break;  // stop
      case '1': targetSpeedPWM =  100; break;  // slow forward
      case '2': targetSpeedPWM =  200; break;  // fast forward
      case '3': targetSpeedPWM =  255; break;  // fastest forward
      case '4': targetSpeedPWM = -100; break;  // slow backward
      case '5': targetSpeedPWM = -200; break;  // fast backward
      case '6': targetSpeedPWM = -255; break;  // fastest backward
    }
    Encoder_1.setTarPWM(-targetSpeedPWM);
    Encoder_2.setTarPWM(targetSpeedPWM);
  }

  // In der loop() Funktion wird die Geschwindigkeit im Motor berechnet
  Encoder_1.loop();
  Encoder_2.loop();

  // alle 100 Millisekunden die Geschwindigkeit ausgeben
  if (millis() > lastOutputMillis + 500) {
    lastOutputMillis = millis();
    Serial.print("Target speed [PWM]:");
    Serial.print(targetSpeedPWM);
    // Erreichte Geschwindigkeit
    Serial.print(",Speed 1:");
    Serial.print(-Encoder_1.getCurrentSpeed());
    Serial.print(",Speed 2:");
    Serial.println(Encoder_2.getCurrentSpeed());
  }
}
```

Dieser Code ist dem obigen sehr ähnlich, nur dass einige Dinge in der Bibliothek gemacht werden. Der eigentliche Steuerungscode wird in der Klasse `MeEncoderOnBoard` gekapselt, wobei für jeden Motor/Encoder ein Klassenobjekt erstellt wird.

Bei der Verknüpfung der Interrupt-Funktionen liefert die Funktion `Encoder_1.getIntNum()` die Interruptnummer für den interruptfähigen Pin:

```c++
// Verknüpfung einer Interrupt-Funktion
attachInterrupt(Encoder_1.getIntNum(), isr_process_encoder1, RISING);
```

Das Hoch-/Runterzählen der Pulse in den Interruptfunktionen ist wie beim bisherigen Beispiel. Die Encoder zählen jetzt also bei jedem Aufruf einen Puls höher oder runter. In der in jedem Durchlauf aufgerufenen Funktion `MeEncoderOnBoard::loop()` wird die Funktion `MeEncoderOnBoard::updateSpeed()` aufgerufen, welche letztlich die Anzahl der Pulse seit dem letzten Aufruf von `updateSpeed()` in die Drehgeschwindigkeit umrechnet.

[NOTE]
====
Die originale Firmware für den Makeblock Bausatz prüft bei Motorfahrt die Geschwindigkeit. Wird trotz Ansteuerung des Motors eine Geschwindigkeit von 0 zurückgeliefert, werden die Motoren ausgeschaltet und ein Beep ertönt. Dies schont wirkungsvoll die Batterien.
====

Die Geschwindigkeit in den Motoren wird mit der Funktion `setTarPWM()` gesetzt. Hier muss man selbst aufpassen, welcher Motor _vorwärts_ und welcher _rückwärts_ eingebaut ist und entsprechend das Vorzeichen ändern. Indem man die Funktion `setTarPWM()` aufruft, wird die Klasse in den direkten Steuerungsmodus PWM gesetzt und stellt die Geschwindigkeit wie bei unserem vorherigen Beispiel via PWM-Duty-Cycle ein. Damit ergibt sich auch eine ähnliche Ausgabe wie bei unserem bisherigen Programm.


## Motorleistung adaptiv regeln mittels in der Encoder-Klasse

Die Ansteuerung via setzen der Ziel-PWM ist die einfachste und direkteste Möglichkeit, den Roboter zu steuern. Aber wie oben erwähnt kann man so kaum eine geforderte Fahrgeschwindigkeit bestimmen. Die Klasse `MeEncoderOnBoard` bietet aber noch eine zweite Variante, wie man die Geschwindigkeit bzw. Motorleistung definieren kann: einen klassischen P-Regler-Ansatz.

Hierbei wird die aktuelle Geschwindigkeit andauernd mit der Sollgeschwindigkeit verglichen. Die Abweichung (der Regelfehler) wird dazu benutzt, um die Leistung/PWM der Motoren solange anzupassen, bis die Ist-Geschwindigkeit der Soll-Geschwindigkeit entspricht und der Regelfehler (nahezu) 0 wird.

.Beispielprogramm für die adaptive Regelung der Geschwindigkeit
```c++
// Testprogramm für die Ansteuerung der DC Motoren via Makeblock Bibliothek
#include <MeAuriga.h>

MeEncoderOnBoard Encoder_1(SLOT1);
MeEncoderOnBoard Encoder_2(SLOT2);

// Interruptfunktion für Encoder 1
void isr_process_encoder1(void) {
  if (digitalRead(Encoder_1.getPortB()) == 0)     Encoder_1.pulsePosMinus();
  else                                            Encoder_1.pulsePosPlus();
}

// Interruptfunktion für Encoder 2
void isr_process_encoder2(void) {
  if (digitalRead(Encoder_2.getPortB()) == 0)     Encoder_2.pulsePosMinus();
  else                                            Encoder_2.pulsePosPlus();
}

int lastOutputMillis;
int targetSpeedRPM;
char buf[4];
int buffIndex;

void setup() {
  // Festlegen der Interruptfunktionen für das Messen/Zählen der Bewegung
  attachInterrupt(Encoder_1.getIntNum(), isr_process_encoder1, RISING);
  attachInterrupt(Encoder_2.getIntNum(), isr_process_encoder2, RISING);
  Serial.begin(115200);
  
  // Interne Timer der PWM auf 8KHz programmieren
  TCCR1A = _BV(WGM10);
  TCCR1B = _BV(CS11) | _BV(WGM12);

  TCCR2A = _BV(WGM21) | _BV(WGM20);
  TCCR2B = _BV(CS21);

  // Die PID Reglerparameter einstellen, eigentlich nur den P-Teil des Reglers, denn der Integral und Differentialteil
  // werden bei der Geschwindigkeitsregelung nicht benutzt.
  Encoder_1.setSpeedPid(0.18, 0, 0);
  Encoder_2.setSpeedPid(0.18, 0, 0);

  // Variable für gelegentliche Ausgaben auf die serielle Schnittstelle
  lastOutputMillis = millis();
  targetSpeedRPM = 0;
  buffIndex = 0;
}

void loop() {
  // von der seriellen Schnittstelle lesen

  // eine maximal 3-stellige Zahl von der seriellen Schnittstelle lesen
  char lastChar;
  while (Serial.available() && buffIndex<4) {
    lastChar = Serial.read();
    // Falls noch weniger < 3 Zeichen und kein Zeilenende, Zeichen in Puffer schieben
    if (lastChar != '\n' && buffIndex != 3)
      buf[buffIndex++] = lastChar;
    else {
      // Zeichenkette in Puffer mit \0 beenden
      buf[buffIndex] = '\0';
      // Text in Zahl umwandeln
      targetSpeedRPM = atoi(buf);
      Serial.print("targetspeed = ");
      Serial.println(targetSpeedRPM);
      // Motorzielgeschwindigkeiten anpassen
      Encoder_1.runSpeed(-targetSpeedRPM);
      Encoder_2.runSpeed(targetSpeedRPM);
      buffIndex = 0;
    }
  }

  // In der loop() Funktion wird die Geschwindigkeit im Motor geregelt
  Encoder_1.loop();
  Encoder_2.loop();

  // alle 100 Millisekunden die Geschwindigkeit und Duty Cycle ausgeben
  if (millis() > lastOutputMillis + 100) {
    lastOutputMillis = millis();
    Serial.print("TargetSpeed:");
    Serial.print(targetSpeedRPM);
    Serial.print(",Speed1:");
    Serial.print(-Encoder_1.getCurrentSpeed());
    Serial.print(",PWM1:");
    Serial.print(-Encoder_1.getCurPwm());
    Serial.print(",Speed2:");
    Serial.print(Encoder_2.getCurrentSpeed());
    Serial.print(",PWM2:");
    Serial.println(Encoder_2.getCurPwm());
  }
}
```

In diesem Programm wird die geforderte Geschwindigkeit über die serielle Schnittstelle gelesen. Im SerialPlotter oder SerialMonitor kann man dazu eine 3-stellige Zahl eingeben. In jedem Schleifendurchlauf wird ein neues Zeichen von der seriellen Schnittstelle in einen Pufferspeicher gelesen, bis entweder ein Zeilenendzeichen (`\n`) folgt, oder das 4. Zeichen gelesen wurde. Dann wird der Text im  Pufferspeicher mit einem `\0` beendet und in eine Zahl gewandelt. Diese wird dann als Soll-Drehzahl übergeben (in der etwas unzutreffenden Funktion `runSpeed()`. Der Controller berechnet in der stets aufgerufenen `MeEncoderOnBoard::loop()`-Funktion den Regelfehler und passt den Duty Cycle (PWM) entsprechend an.

Im Quelltext besteht der wesentliche Unterschied zur ungeregelten Ansteuerung des Motors in der Festlegung der Zielgeschwindigkeit mit der Funktion `runSpeed()`:

```c++
// Motorzielgeschwindigkeiten anpassen
Encoder_1.runSpeed(-targetSpeedRPM);
Encoder_2.runSpeed(targetSpeedRPM);
```

Hier wird die Geschwindigkeit in RPM vorgegeben. Für die Berechnung der Korrektur des PWM-Duty-Cycle wird noch der P-Regler-Faktor benötigt. Den setzt man in der `setup()` Funktion mit `setSpeedPid()`:

```c++
// Die PID Reglerparameter einstellen, eigentlich nur den P-Teil des Reglers, denn der Integral und Differentialteil
// werden bei der Geschwindigkeitsregelung nicht benutzt.
Encoder_1.setSpeedPid(0.18, 0, 0);
Encoder_2.setSpeedPid(0.18, 0, 0);
```

In diesem Programmbeispiel kann die Geschwindigkeit direkt als 3-stellige Zahl angegeben werden. Entsprechend ist das Lesen aus der seriellen Schnittstelle etwas komplexer (Zeichenweise erst in einen Puffer, der dann eine abschließende 0 erhält und dann mit der c-Funktion `atoi()` in eine Zahl gewandelt wird).

Wir testen das wiederum mit verschiedenen Zielgeschwindigkeiten:

.Geregelte Motorleistung, getestet mit 4 Geschwindigkeiten (50, 100, 200, und Max. 340 rpm) sieht das so aus (ein Motor wieder mit Kette, der andere frei drehend):
image::auriga_encoder_speed_8kHz_RPM_controlled.png[]

Man sieht sehr schön, dass der Regler es eigentlich ganz gut schafft, bei beiden Motoren die geforderten Drehzahlen zu erreichen. Nur in der höchsten Stufe schafft es der Motor mit Kette nicht ganz die geforderte Drehzahl zu erreichen, denn er läuft schon konstant am obersten Limit (Duty Cycle 100%). Aufgrund der unterschiedlichen Reibwiderstände muss der Motor mit der Kette (hellblau) stets einen deutlich höheren Duty Cycle fahren, um die Drehzahl zu erreichen.


## Andere Steuerungsfunktionen in der Makeblock-Bibliothek

Statt nur die Motorgeschwindigkeiten zu kontrollieren, kann man mit der Klasse `MeEncoderOnBoard` den _Land Raider_ eine bestimmte Strecke fahren lassen. Das Beispiel in der Makeblock-Bibliothek `Me_Auriga_encoder_pid_pos.ino` zeigt die Verwendung der Klasse `moveTo()`. Ein weiteres Beispiel `Me_Auriga_encoder_callback.ino` zeigt eine Variante dieses Aufrufs, bei der bei Erreichen des Endpunktes eine callback-Funktion aufgerufen wird.

Die Idee der `moveTo()` Funktion ist folgende:

- die Pulse werden in eine zurückgelegte Entfernung umgerechnet und es wird die verbleibende Entfernung bis zum Ziel ausgerechnet
- die Geschwindigkeit wird in Abhängigkeit dieser noch verbleibenden Entfernung gewählt - je weiter weg, umso schneller (allerdings begrenzt durch einen Maximalwert)
- sinkt die verbleibende Strecke, so wird auch die Geschwindigkeit (durch den P-Anteil) reduziert
- der D-Anteil des Reglers wird benutzt, um einem Überschreiten des Zielwertes entgegenzuwirken

Der I-Parameter wird nicht verwendet.


[WARNING]
====
In meinen Tests funktionierte die `moveTo()` Funktion nicht so wirklich gut. Auch fiepten die Motoren nach Erreichen der Zielentfernung weiterhin vor sich hin, wurden also wohl nicht komplett ausgeschaltet. Aber als Ideengrundlage für eine eigene Streckensteuerung kann man das verwenden.
====


## Zusammenfassung

Die Ansteuerung der Motoren selbst ist direkt über das Setzen des PWM-Duty-Cycles und der Richtungsbits sehr einfach möglich. Um aber eine wirklich kontrollierte Fahrt zu programmieren, muss man die Drehgeschwindigkeit mittels Encodern bestimmen. Dazu werden Interruptfunktionen bentuzt, welche Pulse zählen. Diese können dann in Geschwindigkeiten oder zurückgelegte Entfernungen umgerechnet werden.

Die Klasse `MeEncoderOnBoard` der Makeblock-Bibliothek bietet mit der Funktion `runSpeed()` eine P-Regler-kontrollierte Steuerung für die Motorgeschwindigkeit entsprechend einer geforderten Drehzahl. Das ist schon recht bequem. Die Funktion `moveTo()` der Klasse ist aber nicht so toll und sollte besser durch eine eigene Funktion ersetzt werden.

