:imagesdir: ../images

[[chap:rj25]]
# RJ25 Ports und externe Zubehörkomponenten (Abstandssensor und Linienfolgesensor)

## Allgemeines zu RJ25

Die Steckerports am Auriga-Board sind RJ25 (6P6C) Stecker, siehe https://de.wikipedia.org/wiki/RJ-Steckverbindung[Wikipedia RJ-Steckverbindung], wie sie auch für den Anschluss von Modems und Faxgeräten verwendet werden/wurden. Die Belegung der Pins ist im   
link:../downloads/MakeBlock/MeAuriga_Schaltplan.pdf[Schaltplan] zu sehen, wie hier im Auszug für den Port 9:

.RJ25 Belegung an Port 9
image::RJ25_schaltplan_auszug.png[pdfwidth=4cm]

.RJ25 Stecker
image::RJ25_pin_Belegung.png[width=250, pdfwidth=4cm]


Jeder dieser Stecker ist mit VCC und GND verbunden, sowie alle parallel an SDA und SCL (für die Kommunikation über das I2C Protokoll).
Die mit S1 und S2 (Slot 1 und Slot 2) bezeichneten Leitungen sind je Port mit unterschiedlichen Pins am Arduino verbunden. Je nach Fähigkeiten dieser Pins sind die Ports unterschiedlich eingefärbt. Eine Gesamtübersicht über die Ports, deren Belegung und Stromversorgung ist im [Ersten Teil zum Auriga/mBot Ranger](auriga-first-steps-DE) gegeben.

[NOTE]
====
Die Ports sind unterschiedlich abgesichert und bieten unterschiedliche Spannungen. Außerdem sind unterschiedliche Pins angeklemmt, was verschiedene Möglichkeiten zum Anschluss externer Hardware bietet:

.Aurigaplatine mit unterschiedlichen RJ25 Ports
image::auriga_schaubild.jpg[width=500, pdfwidth=12cm]

.Farbkodierung der Anschlüsse (Details dazu im Kapitel zu den RJ25 Ports)
image::farbcodierung_auriga.jpg[width=150, pdfwidth=4cm]

- PORT1 - PORT4 unterstützen einen kontinuierlichen 3,5A-Ausgang (max. 5A)
- PORT1 - PORT4 haben einen Kurzschluss- und Überstromschutz für 3,5A

- PORT5 - PORT10 unterstützen kontinuierlich 5V DC und 4A Ausgang (max. 3A)
- PORT5 - PORT10 haben einen Kurzschluss- und Überstromschutz für 3A
====

An diese Ports kann man nun diverse Zubehörbauteile anstecken (die aber häufig nicht ganz billig sind).

Man kann auch einfach RJ25 Stecker kaufen und über die einzelnen Kabel beliebige elektronische Bauteile anklemmen/anlöten:

.RJ25 Adapter Kabel
image::RJ25-adapter-Kabel.jpg[pdfwidth=4cm]


### Zubehör

MakeBlock bietet selbst zahlreiches Zubehör mit dem RJ25 Stecker an:

.RJ25 Zubehör
image::auriga-rj25-addons.png[pdfwidth=16cm]


## Ultraschall-Entfernungsmessung

Zur Entfernungsmessung liegt im mBot Ranger Bausatz ein Ultraschallsensor dabei. Diesen kann man einfach an einen I2C-Port anstecken (Ports 6-10).

.Ultraschall Entfernungssensor
image::auriga_ultrasonicdistance.png[pdfwidth=4cm]


Wie der Sensor intern funktioniert, ist auf der Seite https://funduino.de/nr-10-entfernung-messen[Fundino Ultraschallsensor]  beschrieben.

In Kürze: Der Sensor emittiert eine Ultraschallwelle (wie die Fledermaus) und wenn diese auf ein Objekt trifft, wird die Welle zurückgeworfen. Die Schallgeschwindigkeit in Luft ist ungefähr 340 m/s (oder 34 cm/ms bzw. 0.034 cm/µs). Der Sensor misst die Zeit zwischen Senden und Empfangen der Schallwelle und kann daraus die Entfernung berechnen.

Falls z.B. die Schallwelle durch ein Objekt zurückgeworfen wird, und seit dem Senden 1000 µs vergangen sind, dann hat die Schallwelle ZUM OBJEKT nur 500 µs benötigt. Das ergibt dann die Entfernung von 500 µs * 0.034 cm/µs = 17 cm.

Der Sensor wird mittels der Klasse `MeUltrasonicSensor` angesteuert, welche folgende Funktionen bietet:

```c++
#include "MeAuriga.h"

// Der Utrasonic-Sensor ist an Steckplatz 6 eingesteckt
MeUltrasonicSensor ultraSensor(PORT_6);

unsigned long lastSamlingMillis;

void setup() {
  lastSamlingMillis = millis();
  Serial.begin(115200);
}

void loop() {
  if (millis() - lastSamlingMillis > 100) {
    Serial.print("Distance,");
    Serial.println(min(120, ultraSensor.distanceCm() ) );
    lastSamlingMillis = millis();
  }

  // ... anderes Zeugs machen...

}
```

[NOTE]
====
Intern verwendet die Makeblock-Bibliothek die Funktion `pulseIn()`, um die Zeit bis zum Eintreffen des Echos zu messen.
====

Zwischen Senden und Empfangen benötigt der Sensor eine minimale Wartezeit von 100 ms.
Wenn man den Sensor also abfragt, und kein `delay()` verwenden möchte (bspw. um nebenbei noch andere
Sensoren abzufragen), dann muss man das Timing manuell implementieren.

[CAUTION]
====
Die Funktion `distanceCm(maxCm)` erlaubt eigentlich die Angabe einer Maximaldistanz, die zurückgeliefert wird, falls das Echo zu lange dauert oder die berechnete Distanz größer als dieser Maximalwert ist. In der aktuellen Implementierung der Bibliothek wird dieses Argument aber ignoriert und stattdessen immer 400 cm (4 m) verwendet, was aber **weit außerhalb** des Messbereichs des Sensors liegt.
Deshalb ist in obigem Messprogramm die maximale Entfernung auf 120 cm begrenzt, was den Messbereich des Sensors bei vielen Hindernissen ganz gut abgrenzt.
====

### Beispielprogramm - LED Ring zeigt gemessene Entfernung an

Man kann auch den LED-Ring nutzen, um die gemessene Entfernung dynamisch zu visualisieren:

```c++
#include "MeAuriga.h"

// Der Utrasonic-Sensor ist an Steckplatz 6 eingesteckt
MeUltrasonicSensor ultraSensor(PORT_6);

unsigned long lastSamplingMillis;

// Der Auriga on-board LED Ring hat 12 LEDs. Zur Vereinfachung der Lesbarkeit
// legen wir hier ein Define fest.
#define LEDNUM  12

// Beim Auriga-Board steuern wird den on-board LED Ring an.
// Die Klasse MeRGBLed bietet die Schnittstellenfunktionen dafür.
// Im Constructor übergeben wir den Port 0
MeRGBLed led( PORT0, LEDNUM );


void setup() {
  lastSamplingMillis = millis();

  // LED Ring Controller ist am PWM PIN D44 angeschlossen
  led.setpin( 44 );
  // Nach dem Setzen des Pin muss minimal kurz gewartet werden.
  // Wenn man den delay()-Aufruf vergisst, dann wird nach dem 
  // led.show() Befehl die erste LED in grün angezeigt.
  delay(1);
  // nun können wir die initalen LED-Werte (alle AUS/SCHWARZ) setzen
  // (Die Klasse MeRGBLed setzt anfänglich alle LEDs auf schwarz/aus, man kann
  //  aber auch led.setColor(0,0,0) vor dem led.show() nochmal aufrufen)
  led.show();

  Serial.begin(115200);
}

void loop() {
  if (millis() - lastSamplingMillis > 100) {
    Serial.print("Distance,");
    long distanceCm = min(120, ultraSensor.distanceCm() );
    Serial.println(distanceCm);
    lastSamplingMillis = millis();

    for (int i=0; i<12; ++i) {
      if (i*10 < distanceCm) {
        int brightness = distanceCm;
        led.setColorAt( i, max(0, 255-i*25), min(255,i*10), 0);
      }
      else
        led.setColorAt( i, 0, 0, 0);
    }
    led.show();
  }

  // ... anderes Zeugs ...

}
```

Mit diesem Programm wird für den Entfernungsbereich 0...120cm der LED Ring beleuchtet. Damit kann man sich ein Bild davon machen, wie der Sensor Hindernisse erkennt. 

[NOTE]
====
Wenn der Ultraschall-Sensor frontal auf eine Wand/Fläche gerichtet ist, so bekommt er ein ziemlich gutes Echo und kann Entfernungen bis über 120 cm sehr gut messen. Aber bereits bei leicht schrägem Einfall wird das Echo am Sensor vorbeireflektiert und dadurch fälschlicherweise ein sehr großer Abstand zurückgeliefert.
====


## Linienverfolgungssensor

Ein weiterer Sensor, der mit dem _MakeBlock Ranger_ Bausatz mitgeliefert wird, ist der Linienverfolgungssensor. 

.Linienverfolgungs-Sensor
image::auriga_linefollower.png[]

Der besteht letztlich aus 2 Photodioden. Deren lichtempfindlicher Widerstand wird mit einem hardwarespezifischen Schwellwert in ein digitales Signal gewandelt, welches 4 Zustände ergibt:

```
S1_IN_S2_IN   : Beide Sensoren innerhalb der schwarzen Linie
S1_IN_S2_OUT  : Sensor 2 ist außerhalb der schwarzen Linie
S1_OUT_S2_IN  : Sensor 1 ist außerhalb der schwarzen Linie
S1_OUT_S2_OUT : Beide Sensoren außerhalb der schwarzen Linie, also entweder links oder rechts davon
```

### Beispielprogramm - Linienfolgesensor zeigt einen der 4 Zustände an

```c++
#include "MeAuriga.h"

// Verbunden mit Port 8
MeLineFollower lineFinder(PORT_8);

void setup() {
  Serial.begin(115200);
}

void loop()
{
  int sensorState = lineFinder.readSensors();
  switch(sensorState)
  {
    case S1_IN_S2_IN  : Serial.println("Beide Sensoren innerhalb der schwarzen Linie"); break;
    case S1_IN_S2_OUT : Serial.println("Sensor 2 ist außerhalb der schwarzen Linie"); break;
    case S1_OUT_S2_IN : Serial.println("Sensor 1 ist außerhalb der schwarzen Linie"); break;
    case S1_OUT_S2_OUT: Serial.println("Beide Sensoren außerhalb der schwarzen Linie"); break;
    default: break;
  }
  delay(200);
}
```

